# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)

pkgname='anomaly'
pkgver=1364850491
pkgrel=1
pkgdesc="[Humble Bundle Android] Tower Offense at its best!"
arch=('x86_64')
url="http://www.anomalythegame.com"
screenshot="http://blog.anomalythegame.com/images/cms/gallerylargephoto206.jpg"
license=('custom')
makedepends=('unzip')
source=(anomaly.desktop
        anomaly.sh)
md5sums=('99525f1aa840faf2c404e77e6bc93400'
         'bb89f6be474294510033d2ac89169c5e')
options=("!strip")

PKGEXT=".pkg.tar"
_archive="AnomalyWarzoneEarth-Installer_Humble_Linux_${pkgver}.zip"
_archive_md5='acf5147293b42c7625aea6ad0e56afc4'
_bundle=('humblebundleandroid' 'humblebundleandroid3')

_humble() {
  if [[ -f "${_gamedir}/${_archive}" ]]; then
    ln -sf "${_gamedir}/${_archive}"
  fi

  if [ ! -f "${_archive}" ]; then
    for bundle in ${_bundle[@]}; do
      case ${bundle} in
        "humblebundle"*) _key="_humblebundle${bundle:12}key";;
        *) continue;;
      esac
      if [ -n "${!_key}" ]; then
        msg2 "Getting your unique ${pkgname} download location."
        _uri="$(curl -s "http://www.humblebundle.com/downloads?key=${!_key}" | grep "${_archive}" | cut -d "'" -f 10)"
        wget $_uri
        mv ${_archive}* ${_archive}
        break
      fi
    done
    if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
      error "Unable to find \"${_archive}\"." 
      plain "You should run one of these commands before installing this pkg."
      for bundle in ${_bundle[@]}; do
        plain "$ export _${bundle}key=<your-${bundle}-key>"
      done
      plain "$ export _gamedir=<dir/to/${_archive}>"
      exit 1
    fi
  fi
  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi
  unset _key _uri _archive_md5 _bundle
}

build() {
  cd "${srcdir}"
  _humble

  bsdtar -xf "${_archive}"
  bsdtar -xf "AnomalyWarzoneEarth-Installer" || true

  unset _archive

  rm data/libgcc_s.so.1
  rm data/libopenal.so.1
  rm data/libstdc++.so.6
}


package() {
  cd "${srcdir}"

  INSTALL_PATH="/opt/anomaly"

  install -d "${pkgdir}/opt/${pkgname}"
  cp -dpr --no-preserve=ownership data/* "${pkgdir}/opt/${pkgname}"

  install -Dm755 anomaly.sh "${pkgdir}/usr/bin/anomaly"
  install -Dm644 anomaly.desktop "${pkgdir}/usr/share/applications/anomaly.desktop"
}

# vim: sts=2 ts=2 sw=2 et
