# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: jbitcm- <chakradeveloper@gmail.com>

pkgname=aptana-studio
_pkgname=aptana
pkgver=3.2.0
pkgrel=1
pkgdesc="A professional, open source development tool for the open web."
arch=("i686" "x86_64")
[[ "$CARCH" == 'i686' ]]   && _arch=x86
[[ "$CARCH" == 'x86_64' ]] && _arch=x86_64
url="http://www.aptana.com/"
screenshot="http://www.aptana.com/images/product/R2-assist-lrg.png"
license=("GPLv3")
depends=("gtk2" "gconf" "java-runtime" "xulrunner" "libjpeg-turbo" "libpng12" "net-tools")
makedepends=("unzip")
options=(!strip)
replaces=("aptana")
conflicts=("aptana")
source=("http://download.aptana.com/studio3/standalone/$pkgver/linux/Aptana_Studio_3_Setup_Linux_${_arch}_$pkgver.zip"
	"aptana.sh"
        "aptana.desktop")
md5sums=(''
         '6b7b28fd865cdaffb66ef5f2a3e175d3'
         'e297d32ab375b84eaaabfca7bc012752')

[[ "$CARCH" == 'i686' ]]   && md5sums[0]='9cbbc506288cd4b8d80897df7952c9fb'
[[ "$CARCH" == 'x86_64' ]] && md5sums[0]='e3b3b6a37490550de8a57b0d701d4138'

package() {
    cd "$srcdir/Aptana_Studio_3"

    local _instpath="/usr/share/$_pkgname"

    # Install Dirs
    find . -type d -exec install -d "{}" "$pkgdir/$_instpath/{}" \; || return 1

    # Install Files
    find . -type f -perm 644 -exec install -Dm644 "{}" "$pkgdir/$_instpath/{}" \; || return 1

    # Install Executables
    find . -type f -perm 755 -exec install -Dm755 "{}" "$pkgdir/$_instpath/{}" \; || return 1

    # install misc
    install -d "$pkgdir/usr/bin" "$pkgdir/usr/share"/{applications,pixmaps} || return 1
    install -m755 "$srcdir/$_pkgname.sh" "$pkgdir/usr/bin/$_pkgname" || return 1
    install -m644 "$srcdir/aptana.desktop" "$pkgdir/usr/share/applications/" || return 1
    ln -s "$_instpath/icon.xpm" "$pkgdir/usr/share/pixmaps/$_pkgname.xpm" || return 1
}
