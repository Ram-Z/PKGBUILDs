# Maintainer: FranzMari <framarinucci@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta

pkgname=awesome
pkgver=3.5.7
pkgrel=1
pkgdesc="A tiling window manager initialy based on a dwm code rewriting."
arch=('x86_64')
url="http://awesome.naquadah.org/"
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'imlib2' 'libxdg-basedir' 'lua' 'lua-lgi'
         'pango' 'startup-notification' 'xcb-util-cursor' 'xcb-util-keysyms'
         'xcb-util-wm' 'xorg-apps')
makedepends=('asciidoc' 'cmake' 'docbook-xsl' 'doxygen' 'imagemagick' 'xmlto')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'vicious: widgets for the Awesome window manager')
backup=('etc/xdg/awesome/rc.lua')
source=("$url/download/$pkgname-$pkgver.tar.xz")
sha256sums=('ba7f92b0ab8b729c569b19b098b0a08339d8654e3c040d07ad02cf99641ceecf')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
      -DCMAKE_BUILD_TYPE=RELEASE \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DSYSCONFDIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # install desktop file so you can start awesome from your login manager
  install -Dm644 "$srcdir"/$pkgname-$pkgver/awesome.desktop \
    "${pkgdir}/usr/share/xsessions/awesome.desktop"
}
