# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>

pkgname=bastion
_pkgname=Bastion
pkgver=20120601
_pkgver=2012-06-01
pkgrel=1
pkgdesc="[Humble Bundle 5] An original action role-playing game set in a lush imaginative world, in which players must create and fight for civilization's last refuge as a mysterious narrator marks their every move."
arch=('x86_64' 'i686')
url="http://supergiantgames.com/?page_id=242"
screenshot="http://supergiantgames.com/site/wp-content/themes/supergiantgames/images/ui/logo.png"
license=('custom')
depends=('libxft')
makedepends=('curl')
source=("${pkgname}.sh" "${pkgname}.desktop")
md5sums=('184f5e38110a9a05a06f7af91b41155a'
         '34a402d17bd34b97f76af9b3a86a14d8')

PKGEXT='.pkg.tar'

_archive="${_pkgname}-HIB-${_pkgver}-1.sh"
_archive_md5="6bfaf5f637a1b54aef066506f6cd6e55"

_bundle=('humblebundle5')

_humble() {
  if [[ -f "${_gamedir}/${_archive}" ]]; then
    ln -sf "${_gamedir}/${_archive}"
  fi

  if [ ! -f "${_archive}" ]; then
    for bundle in ${_bundle[@]}; do
      case ${bundle} in
        "humblebundle"*) _key="_humblebundle${bundle:12}key";;
        *) continue;;
      esac
      if [ -n "${!_key}" ]; then
        msg2 "Getting your unique ${pkgname} download location."
        _uri="$(curl -s "http://www.humblebundle.com/downloads?key=${!_key}" | grep "${_archive}" | cut -d "'" -f 10)"
        wget $_uri -O ${_archive}
        break
      fi
    done
    if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
      error "Unable to find \"${_archive}\"." 
      plain "You should run one of these commands before installing this pkg."
      for bundle in ${_bundle[@]}; do
        plain "$ export _${bundle}key=<your-${bundle}-key>"
      done
      plain "$ export _gamedir=<dir/to/${_archive}>"
      exit 1
    fi
  fi
  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi
  unset _key _uri _archive_md5 _bundle
}

build() {
  cd "${srcdir}"
  _humble

  chmod +x $_archive
}

package () {
  cd "${srcdir}"

  install -dm755 "${pkgdir}/opt/"
  ./${_archive} -u --packager pacman --datadir "${pkgdir}/opt" --bindir "${pkgdir}/opt/Bastion"

  cd "${pkgdir}/opt/Bastion"

  [[ $CARCH == 'i686' ]] && sed -i 's/x86_64/i386/' "${srcdir}/${pkgname}.sh"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}

# vim: sts=2 ts=2 sw=2 et
