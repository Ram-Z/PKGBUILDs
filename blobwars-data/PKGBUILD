# Maintainer: Samir Benmendil <ram-z@chakraproject.org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: geekitus

pkgname=blobwars-data
_pkgname=blobwars
pkgver=1.19
pkgrel=2
pkgdesc="Platform action game featuring a blob wih lot of weapons - data files"
arch=('any')
url="http://sourceforge.net/projects/blobwars"
license=('GPL')
makedepends=('sdl_image' 'sdl_mixer' 'sdl_ttf' 'sdl_net')
source=("http://downloads.sourceforge.net/project/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('87e8aaa114b6e4742ea6ee28e7a7f984')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # fix building with gcc 4.6
  sed -i -e 's/-Werror/-Werror -Wno-error=unused-result/g' makefile

  RELEASE=1 make 
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make PREFIX="${pkgdir}/usr" BINDIR="${pkgdir}/usr/bin/" install

  # We remove all except data files
  rm -r "${pkgdir}"/usr/{bin,share/{applications,doc,icons,locale}}
}
