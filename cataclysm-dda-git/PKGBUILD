# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# -*- shell-script -*-
# Contributor: Fernando Carmona Varo <ferkiwi @t gmail dot com>
# Contributor: Klemen Ko≈°ir <klemen913@gmail.com>

pkgname=cataclysm-dda-git
pkgver=0.C.r22069.g491bcd9
pkgrel=1
pkgdesc="Cataclysm: Dark Days Ahead is an actively maintained roguelike set in a post-apocalyptic world. Both SDL tiles and ascii version included."
arch=('i686' 'x86_64')
url="http://www.cataclysmdda.com/"
license=('CCPL:by-sa')
conflicts=('cataclysm-dda', 'cataclysm-dda-ncurses')
depends=('ncurses' 'sdl2_image' 'sdl2_ttf' 'sdl2_mixer' 'luajit')
makedepends=('git' 'gettext')
install=
source=("$pkgname"::'git://github.com/CleverRaven/Cataclysm-DDA.git'
        "cataclysm-dda.desktop")
md5sums=('SKIP'
         '6a0a2a10583c3909230c798ee6ad622f')

pkgver() {
  cd $pkgname
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "${pkgname}"
  make PREFIX=/usr RELEASE=1 ZLEVELS=1 USE_XDG_DIR=1 LUA=1
  make PREFIX=/usr RELEASE=1 ZLEVELS=1 USE_XDG_DIR=1 LUA=1 TILES=1 SOUND=1
}

package() {
  cd "${pkgname}"

  local instdir=/usr/share/cataclysm-dda

  make install DESTDIR="$pkgdir" PREFIX=/usr TILES= SOUND= LUA=1

  make install DESTDIR="$pkgdir" PREFIX=/usr TILES=1 SOUND=1 LUA=1

  # .desktop
  install -dm755 "$pkgdir/usr/share/applications"
  cp "$srcdir/cataclysm-dda.desktop" "$pkgdir/usr/share/applications"

  # icon
  install -D 'data/osx/AppIcon.iconset/icon_128x128.png' "$pkgdir/usr/share/icons/hicolor/128x128/apps/cataclysm-dda.png"

  # doc
  install -dm755 "$pkgdir/usr/share/doc/cataclysm-dda"
  cp -r --no-preserve=ownership doc/* "$pkgdir/usr/share/doc/cataclysm-dda/"
  rm "$pkgdir/usr/share/doc/cataclysm-dda/JSON_LOADING_ORDER.md"
  cp 'data/json/LOADING_ORDER.md' "$pkgdir/usr/share/doc/cataclysm-dda/JSON_LOADING_ORDER.md"

  # License file
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
