# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributer: Gadget3000 <gadget3000 at msn dot com>
# Contributor: jimmy-6 <jakub.jarozek at gmail dot com>

pkgname='cogs'
pkgver='20110811'
pkgrel=1
pkgdesc="[Humble Bundle 3,4,Android2] A game in which you build machines from sliding tiles"
arch=('i686' 'x86_64')
 [ $CARCH = 'i686' ]   && _arch='x86'
 [ $CARCH = 'x86_64' ] && _arch='amd64'
url="http://www.cogsgame.com/"
license=('unknown: commercial')
depends=('gcc-libs' 'sdl<1.3' 'openal')
makedepends=('unzip' 'imagemagick')
conflicts=('cogs-hib')
source=("cogs.desktop" "cogs.sh")
md5sums=('a35a1fc9e81e3ab180cc2fe523e5ad43'
         'fc528c550cac9cdf84f3672eb0ef6ffa')
PKGEXT=".pkg.tar"

_archive="${pkgname}_${pkgver}_all.tar.gz"
_archive_md5="d7617317c87bbf0a3f0497f28b5b7e8d"

_bundle=('humblebundle3' 'humblebundle4' 'humblebundleandroid2')

_humble() {
  if [ ! -f "${_archive}" ]; then
    for bundle in ${_bundle[@]}; do
      case ${bundle} in
        "humblebundle"*) _key="_humblebundle${bundle:12}key";;
        *) continue;;
      esac
      if [ -n "${!_key}" ]; then
        echo "Getting your unique ${pkgname} download location."
        _uri="$(curl -s "http://www.humblebundle.com/downloads?key=${!_key}" | grep "${_archive}" | cut -d "'" -f 10)"
        wget $_uri
        mv ${_archive}* ${_archive}
        break
      else
        echo "\"${_key}\" environment variable required for download handling not found."
      fi
    done
    if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
      echo "Unable to download \"${_archive}\"."
      exit 1
    fi
  fi
  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi
  unset _key _uri _archive_md5
}

build() {
  cd "${srcdir}"
  _humble

  tar xzf "${_archive}" || true

  unset _archive
}

package() {
  cd "${srcdir}/${pkgname}"

  install -Dd "${pkgdir}/opt/${pkgname}"

  cp -dpr --no-preserve=ownership "data" "${pkgdir}/opt/${pkgname}"

  #Create launcher
  install -Dm755 "${srcdir}/cogs.sh" "${pkgdir}/usr/bin/cogs"

  install -Dm755 "Cogs-${_arch}" "Cogs" "${pkgdir}/opt/${pkgname}"
  install -Dm644 "cogs.png" "cogs-icon.bmp" "README-linux.txt" "${pkgdir}/opt/${pkgname}"

  for i in 16x16 22x22 32x32 48x48 64x64 128x128; do
    mkdir -p ${pkgdir}/usr/share/icons/hicolor/${i}/apps
    convert -resize ${i} \
        ${pkgdir}/opt/cogs/cogs.png \
        ${pkgdir}/usr/share/icons/hicolor/${i}/apps/cogs.png
  done

  # install menu entry
  install -D ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
}

# vim: sts=2 ts=2 sw=2 et
