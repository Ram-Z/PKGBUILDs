# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: tailinchu <use_my_id at gmail dot com>

pkgname=connman-git
pkgver=1.24.89.gd60623c
pkgrel=1
pkgdesc="Wireless LAN network manager (git version)"
arch=('i686' 'x86_64')
url="http://connman.net/"
license=('GPL2')
depends=('dbus-core' 'iptables' 'glib2' 'wpa_supplicant' 'gnutls' 'pptpclient')
makedepends=('git')
conflicts=('connman')
provides=('connman')
source=("git://git.kernel.org/pub/scm/network/connman/connman.git")
md5sums=('SKIP')

_gitroot="connman"

pkgver () {
  cd "$srcdir/$_gitroot"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_gitroot"
  ./bootstrap
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --with-systemdunitdir=/usr/lib/systemd/system \
    --enable-threads \
    --enable-pptp \
    --enable-polkit \
    --enable-client
  make
}

package() {
  cd "$srcdir/$_gitroot"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/$_gitroot/client/connmanctl" "$pkgdir/usr/bin/connmanctl"
  find "$pkgdir/usr" -name \*.service -exec sed -i 's/s\(bin\)/\1/' {} +
}
