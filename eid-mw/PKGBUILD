# Maintainer: Samir Benmendil <samir.benmendil@gmail.com>

pkgname=eid-mw
pkgver=4.0.6_1620
_build=tcm406-258906
pkgrel=1
pkgdesc="The eID middleware for the Belgian eID"
arch=('i686' 'x86_64')
url="http://eid.belgium.be/"
license=('LGPL3')
depends=('pcsclite' 'gtk2')
makedepends=('ccache' 'java-runtime')
source=("http://eid.belgium.be/en/binaries/${pkgname}-${pkgver//_/-}.tar_${_build}.gz")
md5sums=('4de30e6ad4e7e8f5510a89cd8647012e')
options=(!libtool)

build() {
  cd $pkgname-${pkgver%_*}
  ./configure --prefix=/usr --libexecdir=/usr/bin --disable-static
  make
}

check() {
  cd $pkgname-${pkgver%_*}
  make -k check
}

package() {
  cd $pkgname-${pkgver%_*}
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
