# Maintainer: Samir Benmendil <samir.benmendil@gmail.com>

pkgname=eid-mw
pkgver=4.1.21
pkgrel=1
pkgdesc="The eID middleware for the Belgian eID"
arch=('i686' 'x86_64')
url="http://eid.belgium.be/"
license=('LGPL3')
depends=('pcsclite' 'gtk2')
optdepends=('firefox: extension for Belgian eid'
            'acsccid: ACS CCID smart card readers'
            'ccid: A generic USB Chip/Smart Card Interface Devices driver'
            'pcsc-tools: PC/SC smartcard tools')
source=("https://dist.eid.belgium.be/continuous/sources/$pkgname-$pkgver-v$pkgver.tar.gz"{,.asc})
md5sums=('651ff5056cf85d447c79dd44649f26c4'
         'SKIP')
validpgpkeys=('D95426E309C0492990D8E8E2824A5E0010A04D46')

build() {
  cd $pkgname-$pkgver-v$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/bin --disable-static
  make
}

check() {
  cd $pkgname-$pkgver-v$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver-v$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
