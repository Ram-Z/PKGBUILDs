# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Simon Gomizelj <simongmzlj@gmail.com>

pkgname=envoy
pkgver=14
pkgrel=1
pkgdesc="A ssh-agent/gpg-agent keychain and process monitor"
arch=('i686' 'x86_64')
url="http://github.com/vodik/envoy"
license=('GPL')
depends=('openssh' 'systemd')
optdepends=('gnupg: gpg-agent support')
makedepends=('ragel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/vodik/$pkgname/archive/v$pkgver.tar.gz")
md5sums=('4130f2080ac1a083607e6764a59fd02c')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/libsystemd-daemon/libsystemd/g' Makefile
}

build() {
  make -C "$pkgname-$pkgver"
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
