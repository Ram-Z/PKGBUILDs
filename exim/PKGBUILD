# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=exim
pkgver=4.77
pkgrel=1
pkgdesc="Message Transfer Agent"
arch=('x86_64' 'i686')
url="http://www.exim.org/"
license=('GPL')
backup=(etc/mail/aliases etc/mail/exim.conf \
        etc/logrotate.d/exim etc/conf.d/exim)
install=exim.install
changelog=ChangeLog
depends=('db' 'pcre' 'pam' 'openssl' 'libldap')
provides=('smtp-server' 'smtp-forwarder')
conflicts=('smtp-server' 'smtp-forwarder')
options=('!makeflags')
source=("http://mirror.switch.ch/ftp/mirror/exim/exim/exim4/exim-$pkgver.tar.bz2"
        aliases
        exim
        exim.logrotate
        exim.conf.d
        exim.Makefile)
sha256sums=('0ccc13cf2f052b1163fcdf71c55a3578765050848ba413a6473d3ab5d20b1475'
            '932c9149b6809c70e94c1256e28325d197bbf80d27322793e217d4b692d49c5a'
            '48457622b22de9721efe9f143a88e5f5ce833b792d2e757237b6cdb74a1708e6'
            '7f1408f9c5d905968e665941f5c5efcf9da53e7a0bbef6c66220343bc2ae994b'
            '0209c701f8696a9628d43c1684105eadd35d1caba199b94e3a54a4d26cecff01'
            '7d1e062c907a06293eee40a3139f70c74a055374b234fb8c431ea68362bdda55')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cp "$srcdir/$pkgname.Makefile" Local/Makefile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm644 ../exim.logrotate "${pkgdir}/etc/logrotate.d/exim"
  install -Dm644 ../exim.conf.d "${pkgdir}/etc/conf.d/exim"
  install -Dm644 doc/exim.8 "${pkgdir}/usr/share/man/man8/exim.8"
  install -Dm755 ../exim "${pkgdir}/etc/rc.d/exim"
  mkdir -p "${pkgdir}/var/spool/exim/db" "${pkgdir}/etc/mail" \
    "${pkgdir}/var/log/exim" "${pkgdir}/usr/"{lib,sbin}
  chmod 770 "${pkgdir}/var/spool/exim" "${pkgdir}/var/spool/exim/db" "${pkgdir}/var/log/exim"
  cd build-Linux-*
  for i in exicyclog exim_checkaccess exim_dumpdb exim_lock\
          exim_tidydb exipick exiqsumm exigrep exim_dbmbuild exim\
          exim_fixdb eximstats exinext exiqgrep exiwhat; do
          install -m 0755 "$i" "$pkgdir/usr/sbin"
  done

  cd "$srcdir/exim-$pkgver/src"
  sed -e "s|/etc/aliases|/etc/mail/aliases|g" \
      -e "s|SYSTEM_ALIASES_FILE|/etc/mail/aliases|g" configure.default \
      >"$pkgdir/etc/mail/exim.conf"

  cp "$srcdir/aliases" "$pkgdir/etc/mail"
  cd "$pkgdir/usr/sbin"
  for i in mailq rmail rsmtp runq sendmail; do
      ln -s exim "$i"
  done
  # fhs compliancy
  ln -s ../sbin/exim ../lib/sendmail

  mkdir -p "$pkgdir/etc/rc.d"
  cp "$srcdir/exim" "$pkgdir/etc/rc.d"
}

# vim:set ts=2 sw=2 et:
