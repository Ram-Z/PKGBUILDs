# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Andrea Repetto <andrea.repetto85@gmail.com>
# Contributor: Bogdan Szczurek <thebodzio@gmail.com>

_gitname=Flexget

pkgname=flexget-git
pkgver=1.2.125
pkgrel=1
pkgdesc="Automate downloading or processing content (torrents, podcasts, etc.)"
arch=("any")
url="http://flexget.com/"
depends=('python2' 'python2-feedparser>=5.1.3' 'python2-sqlalchemy'
         'python2-yaml' 'python2-beautifulsoup4>=4.1' 'python2-html5lib>=0.11'
         'python2-pyrss2gen' 'python2-pynzb' 'python2-progressbar' 'python2-jinja'
         'python2-flask' 'python2-cherrypy' 'python2-requests>=1.0'
         'python2-dateutil' 'python2-jsonschema>=2.0.0' 'python2-tvrage'
         'python2-tmdb3')
optdepends=('python2-transmissionrpc: Transmission support')
makedepends=('git' 'python2-setuptools' 'python2-paver')
license=(MIT)
source=("git://github.com/$_gitname/$_gitname.git")
conflicts=('flexget')
md5sums=('SKIP')

pkgver() {
  cd $srcdir/$_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $srcdir/$_gitname

  msg "Patching shebangs"
  sed -i 's/\(python\)/\12/' flexget{,/ui}/__init__.py
}

package() {
  cd $srcdir/$_gitname

  python2 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=1 || return 1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
