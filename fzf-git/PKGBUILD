# Maintainer: Ram-Z <ram-z@chakra-project.org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>
# Contributor: jebaum <jebaum at ucla dot edu>

pkgname=fzf-git
_pkgname=fzf
pkgver=0.11.4.r16.ge5ac2eb
pkgrel=1
pkgdesc="Command-line fuzzy finder"
arch=('i686' 'x86_64')
depends=('ncurses' 'tmux')
makedepends=('git' 'go' 'make')
optdepends=('fish: fish keybindings'
            'fzf-extras: fzf keybindings from junegunn wiki and dotfiles'
            'vim-fzf: fzf.vim'
            'zsh: zsh keybindings')
url="https://github.com/junegunn/fzf"
license=('MIT')
source=(git+https://github.com/junegunn/fzf
        key-bindings.bash.patch
        key-bindings.zsh.patch)
sha256sums=('SKIP'
            'fc981d036d85d0b9a5e6ba65f84b1c0f86bcee2dabb09238e1edfa1f49a85b63'
            '0b89bf1d6d372b9a8d5927e58151157d42848086a252979006b89b72242c52e6')
install=fzf.install

pkgver() {
  cd $_pkgname
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "$_pkgname"

  msg2 'Fixing bash and zsh key bindings...'
  patch -p1 < "$srcdir/key-bindings.bash.patch"
  patch -p1 < "$srcdir/key-bindings.zsh.patch"
}

build() {
  cd "$_pkgname"

  msg2 'Building...'
  GOPATH="$srcdir" TMPDIR=/tmp go get -u github.com/junegunn/fzf/...
}

package() {
  cd "$_pkgname"

  msg2 'Installing documentation...'
  install -dm755 "$pkgdir"/usr/share/doc/fzf
  install -m644 install uninstall README.md "$pkgdir"/usr/share/doc/fzf

  msg2 'Installing manual...'
  install -Dm 644 "man/man1/fzf.1" "$pkgdir"/usr/share/man/man1/fzf.1

  msg2 'Installing executables...'
  install -dm 755 "$pkgdir"/usr/bin
  install -Dm 755 "$srcdir"/bin/fzf bin/fzf-tmux "$pkgdir"/usr/bin/

  # msg2 'Installing bash completion and keybindings...'
  # install -Dm 644 "shell/completion.bash" "$pkgdir"/usr/share/bash-completion/completions/fzf
  # install -Dm 755 "shell/key-bindings.bash" "$pkgdir"/etc/profile.d/fzf.bash
  #
  # msg2 'Installing fish keybindings...'
  # install -Dm 644 "shell/key-bindings.fish" "$pkgdir"/usr/share/fish/functions/fzf.fish

  msg2 'Installing zsh completion and keybindings...'
  install -Dm 644 "shell/completion.zsh" "$pkgdir"/usr/share/zsh/site-functions/_fzf
  install -Dm 755 "shell/key-bindings.zsh" "$pkgdir"/etc/profile.d/fzf.zsh
}
