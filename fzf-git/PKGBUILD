# Maintainer: Ram-Z <ram-z@chakra-project.org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>
# Contributor: jebaum <jebaum at ucla dot edu>

pkgname=fzf-git
_pkgname=fzf
pkgver=0.9.11.r16.g0937bd6
pkgrel=1
pkgdesc="Command-line fuzzy finder"
arch=('i686' 'x86_64')
depends=('ncurses' 'tmux')
makedepends=('git' 'go' 'make')
optdepends=('fish: fish keybindings'
            'fzf-extras: fzf keybindings from junegunn wiki and dotfiles'
            'vim-fzf: fzf.vim'
            'zsh: zsh keybindings')
url="https://github.com/junegunn/fzf"
license=('MIT')
source=(git+https://github.com/junegunn/fzf
        key-bindings.bash.patch
        key-bindings.zsh.patch)
sha256sums=('SKIP'
            'fae015d42244211c13655539240fcff6118c466ec9aaf3c136d3e70d733c90d0'
            'efbd3abb647dffc75a3da5ce759e5b8f1a20c2e8addf4c1bb8964befdbb1916b')
install=fzf.install

pkgver() {
  cd $_pkgname
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "$_pkgname"

  msg2 'Fixing bash and zsh key bindings...'

  msg2 'Making fzf key-bindings.bash source bash completions...'
  patch -p1 < "$srcdir/key-bindings.bash.patch"

  msg2 'Making fzf key-bindings.zsh source zsh completions...'
  patch -p1 < "$srcdir/key-bindings.zsh.patch"

  msg2 'Removing instances of "source ~/.fzf.bash"...'
  sed -i 's@source ~/.fzf.bash;\s@@' shell/key-bindings.bash

  msg2 'Removing instances of "source ~/.fzf.zsh"...'
  sed -i 's@source ~/.fzf.zsh;\s@@' shell/key-bindings.zsh
}

build() {
  cd "$_pkgname"

  msg2 'Building...'
  GOPATH="$srcdir" TMPDIR=/tmp go get -u github.com/junegunn/fzf/...
}

package() {
  cd "$_pkgname"

  msg2 'Installing documentation...'
  for _doc in install uninstall README.md; do
    install -Dm 644 $_doc "$pkgdir/usr/share/doc/fzf/$_doc"
  done

  msg2 'Installing manual...'
  install -Dm 644 "man/man1/fzf.1" "$pkgdir/usr/share/man/man1/fzf.1"

  msg2 'Installing executables...'
  install -Dm 755 "$srcdir/bin/fzf" "$pkgdir/usr/bin/fzf"
  install -Dm 755 "bin/fzf-tmux" "$pkgdir/usr/bin/fzf-tmux"

  msg2 'Installing bash completion and keybindings...'
  install -Dm 644 "shell/completion.bash" "$pkgdir/usr/share/bash-completion/completions/fzf"
  install -Dm 755 "shell/key-bindings.bash" "$pkgdir/etc/profile.d/fzf.sh"

  msg2 'Installing fish keybindings...'
  install -Dm 644 "shell/key-bindings.fish" "$pkgdir/usr/share/fish/functions/fzf.fish"

  msg2 'Installing zsh completion and keybindings...'
  install -Dm 755 "shell/key-bindings.zsh" "$pkgdir/etc/profile.d/fzf.zsh"
}
