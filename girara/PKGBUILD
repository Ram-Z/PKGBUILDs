# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.2.9
pkgrel=1
pkgdesc="User interface library focused on simplicity and minimalism"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/girara"
license=('custom')
depends=('gtk3')
makedepends=('meson' 'ninja')
replaces=('girara-gtk3' 'girara-common')
provides=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')
source=("http://pwmt.org/projects/girara/download/girara-$pkgver.tar.xz")
sha1sums=('69d20c2f06c47ef51feb34148ef8c4985b9e8a77')

build() {
  cd "girara-$pkgver"
  chakra-meson build

  cd build
  ninja
}

package() {
  cd "girara-$pkgver"/build
  DESTDIR="$pkgdir/" ninja install

  install -D -m664 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
