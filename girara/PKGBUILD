# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.2.4
pkgrel=1
pkgdesc="User interface library focused on simplicity and minimalism"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/girara"
license=('custom')
depends=('gtk3')
replaces=('girara-gtk3' 'girara-common')
provides=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')
source=("http://pwmt.org/projects/girara/download/girara-$pkgver.tar.gz")
md5sums=('f22a75b51cf88f241096e230985f28db')

build() {
  cd "$srcdir/girara-$pkgver"
  touch colors.mk
  make GIRARA_GTK_VERSION=3
}

package() {
  cd "$srcdir/girara-$pkgver"
  make GIRARA_GTK_VERSION=3 DESTDIR="$pkgdir/" install
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
