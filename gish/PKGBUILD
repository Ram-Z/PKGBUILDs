# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributer: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)
# Contributer: Carl Reinke <mindless2112 gmail com>
# Contributor: Artificial Intelligence <polarbeard@gmail.com>

pkgname=gish
pkgver=1.61
pkgrel=1
pkgdesc="[Humble Bundle 1,2,Voxatron] A one of a kind 2d sidescroller with a twist you play as a totally physics based ball of tar."
arch=('i686' 'x86_64')
url='http://www.chroniclogic.com/gish.htm'
screenshot="http://www.chroniclogic.com/image/gish/gish-11.jpg"
license=('custom: "commercial"')
install=gish.install
depends=('libvorbis' 'openal' 'sdl' 'gcc-libs' 'sdl_image' 'mesa')
conflicts=('gish')
source=('gish.desktop'
        'gish.sh')
md5sums=('11a6fe4611d75e8cc94c2596d8b81746'
         '1deb28366976a258530faa11baac2391')
options=('!strip')
PKGEXT='.pkg.tar' 

_archive='gish_1.6.1_all.tar.gz'
_archive_md5='c2c842b0a19353113069db5c8fe8bc07'
_bundle=('humblebundle1' 'humblebundle2' 'humblebundlevoxatron')

_humble() {
  if [[ -f "${_gamedir}/${_archive}" ]]; then
    ln -sf "${_gamedir}/${_archive}"
  fi

  if [ ! -f "${_archive}" ]; then
    for bundle in ${_bundle[@]}; do
      case ${bundle} in
        "humblebundle"*) _key="_humblebundle${bundle:12}key";;
        *) continue;;
      esac
      if [ -n "${!_key}" ]; then
        msg2 "Getting your unique ${pkgname} download location."
        _uri="$(curl -s "http://www.humblebundle.com/downloads?key=${!_key}" | grep "${_archive}" | cut -d "'" -f 10)"
        wget $_uri
        mv ${_archive}* ${_archive}
        break
      fi
    done
    if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
      error "Unable to find \"${_archive}\"." 
      plain "You should run one of these commands before installing this pkg."
      for bundle in ${_bundle[@]}; do
        plain "$ export _${bundle}key=<your-${bundle}-key>"
      done
      plain "$ export _gamedir=<dir/to/${_archive}>"
      exit 1
    fi
  fi
  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi
  unset _key _uri _archive_md5 _bundle
}

build() {
  cd "${srcdir}"
  _humble

  tar xzf "${_archive}" || true
  unset _archive _archive_md5 _uri
}

package()
{
  cd "${srcdir}/${pkgname}"

	install -dm775 -ggames "$pkgdir/opt/gish"
	
	cp -rdp --no-preserve=ownership * "$pkgdir/opt/gish/"
	
	install -Dm644 "gish.bmp"     "$pkgdir/usr/share/pixmaps/gish.bmp"
	install -Dm644 "${srcdir}/gish.desktop" "$pkgdir/usr/share/applications/gish.desktop"
	
	if [ "$CARCH" = "x86_64" ]; then
    rm gish_32
    mv gish_64 gish
	else
    rm gish_64
    mv gish_32 gish
	fi
  install -Dm755 "${srcdir}/gish.sh" "$pkgdir/usr/bin/gish"
}

# vim: sts=2 ts=2 sw=2 et
