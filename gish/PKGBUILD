# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributer: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)
# Contributer: Carl Reinke <mindless2112 gmail com>
# Contributor: Artificial Intelligence <polarbeard@gmail.com>

pkgname=gish
pkgver=1.61
pkgrel=1
pkgdesc="[Humble Bundle 1,2,Voxatron] A one of a kind 2d sidescroller with a twist you play as a totally physics based ball of tar."
arch=('i686' 'x86_64')
url='http://www.chroniclogic.com/gish.htm'
license=('custom: "commercial"')
install=gish.install
depends=('libvorbis' 'openal' 'sdl' 'gcc-libs' 'sdl_image' 'mesa')
conflicts=('gish')
source=('gish.png'
        'gish.desktop'
        'gish32bit.launcher'
        'gish64bit.launcher')
md5sums=('f3831056f039b0d6867781d21b857993'
         'cb2f9c719e432d0d8195860f5831fe0d'
         'eafe7f167cba997cfccecae6f5ac4956'
         '303264ff383435c4c8f6843a64c51f62')
options=('!strip')
PKGEXT='.pkg.tar' 

_archive='gish_1.6.1_all.tar.gz'
_archive_md5='c2c842b0a19353113069db5c8fe8bc07'
_bundle=('humblebundle1' 'humblebundle2' 'humblebundlevoxatron')

_humble() {
  for bundle in ${_bundle[@]}; do
    case ${bundle} in
      "humblebundle"*) _key="_humblebundle${bundle:12}key";;
      *) continue;;
    esac
    if [ -n "${!_key}" ]; then
      echo "Getting your unique ${pkgname} download location."
      _uri="$(curl -s "http://www.humblebundle.com/downloads?key=${!_key}" | grep "${_archive}" | cut -d "'" -f 10)"
      break
    else
      echo "\"${_key}\" environment variable required for download handling not found."
    fi
  done
  if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
    echo "Unable to download \"${_archive}\"."
    exit 1
  fi
  unset _key
}

build() {
  _humble

  cd "${srcdir}"
  if [ ! -f "${_archive}" ]; then
    wget $_uri
    mv ${_archive}* ${_archive}
  fi
  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi
  tar xzf "${_archive}" || true
  unset _archive _archive_md5 _uri
}

package()
{
	install -dm775 -ggames $pkgdir/opt/Gish
	
	cp -r $srcdir/gish/* $pkgdir/opt/Gish/
	chown -R :games $pkgdir/opt/Gish/*
	chmod -R g+rw $pkgdir/opt/Gish/*
	chmod -R g-w+x $pkgdir/opt/Gish/gish_{32,64}
	
	install -Dm644 $srcdir/gish.png     $pkgdir/usr/share/pixmaps/gish.png 
	install -Dm644 $srcdir/gish.desktop $pkgdir/usr/share/applications/gish.desktop 
	
	if [ "$CARCH" = "x86_64" ]; then
		install -Dm755 $srcdir/gish64bit.launcher $pkgdir/usr/bin/gish 
	else
		install -Dm755 $srcdir/gish32bit.launcher $pkgdir/usr/bin/gish 
	fi
}

# vim: sts=2 ts=2 sw=2 et
