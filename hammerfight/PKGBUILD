# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributer: Gadget3000 <gadget3000 at msn dot com>

pkgname=hammerfight
pkgver=1.004
pkgrel=1
pkgdesc="[Humble Bundle 3&4] A 2D physics-based video game in which you swing large melee weapons to destroy enemies"
arch=('i686' 'x86_64')
 [[ $CARCH == 'i686' ]]   && _arch=x86
 [[ $CARCH == 'x86_64' ]] && _arch=amd64
url="http://www.koshutin.com/"
screenshot="http://www.koshutin.com/Media/Screens/screen_01.jpg"
license=('unknown')
makedepends=('imagemagick')
source=("hammerfight.desktop"
        "hammerfight.sh")
md5sums=('e23ba95fa5623d37bdd0f3b520e36343'
         '802b2cabf5638b2b7d35b6af369bfff0')
install='hammerfight.install'
depends=('openal' 'gcc-libs' 'sdl' 'hicolor-icon-theme')
options=('!strip')
PKGEXT='.pkg.tar'

_archive="hammerfight_1.004_all.tar.gz"
_archive_md5="cc3fd17aa5849b0de3f0aa967ea8afe2"

_humble() {
  if [[ -f "${_gamedir}/${_archive}" ]]; then
    ln -sf "${_gamedir}/${_archive}"
  fi

  if [ ! -f "${_archive}" ]; then
    for bundle in ${_bundle[@]}; do
      case ${bundle} in
        "humblebundle"*) _key="_humblebundle${bundle:12}key";;
        *) continue;;
      esac
      if [ -n "${!_key}" ]; then
        msg2 "Getting your unique ${pkgname} download location."
        _uri="$(curl -s "http://www.humblebundle.com/downloads?key=${!_key}" | grep "${_archive}" | cut -d "'" -f 10)"
        wget $_uri
        mv ${_archive}* ${_archive}
        break
      fi
    done
    if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
      error "Unable to find \"${_archive}\"." 
      plain "You should run one of these commands before installing this pkg."
      for bundle in ${_bundle[@]}; do
        plain "$ export _${bundle}key=<your-${bundle}-key>"
      done
      plain "$ export _gamedir=<dir/to/${_archive}>"
      exit 1
    fi
  fi
  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi
  unset _key _uri _archive_md5 _bundle
}

build() {
  cd "${srcdir}"
  _humble

  tar xzf "${_archive}" || true

  unset _archive
}

package() {
  cd "${srcdir}/${pkgname}"

  install -Dd "${pkgdir}/opt/hammerfight"
  cp -rdp --no-preserve=ownership Data Objects Media Saves "${pkgdir}/opt/${pkgname}"
  
  install -Dm644 Config.ini README-linux.txt hammerfight.png hgeicon.bmp media.script readme.txt strings.txt \
    "${pkgdir}/opt/${pkgname}"
  install -Dm755 Hammerfight-${_arch} "${pkgdir}/opt/${pkgname}/Hammerfight"

  install -Dd "${pkgdir}/usr/bin"
  install -m755 "${srcdir}/hammerfight.sh" "${pkgdir}/usr/bin/hammerfight"

  for i in 16x16 22x22 32x32 48x48 64x64 128x128; do
    mkdir -p "${pkgdir}/usr/share/icons/hicolor/${i}/apps"
    convert -resize ${i} \
	"${pkgdir}/opt/hammerfight/hammerfight.png" \
	"${pkgdir}/usr/share/icons/hicolor/${i}/apps/hammerfight.png"
  done

  install -D "${srcdir}/hammerfight.desktop" "${pkgdir}/usr/share/applications/hammerfight.desktop"
}

# vim: sts=2 ts=2 sw=2 et
