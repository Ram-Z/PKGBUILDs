# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributer: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)
# Contributer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban

pkgname=jdk7
_pkgname=jdk
pkgver=7u9
_build=b05
pkgrel=1
pkgdesc="Java 7 Development Kit"
url="http://www.oracle.com/technetwork/java/javase/downloads/index.html"
arch=('i686' 'x86_64')
 [ $CARCH = 'i686' ]   && _arch='i586' && _arch2='i386'
 [ $CARCH = 'x86_64' ] && _arch='x64'  && _arch2='amd64'
license=('custom')
depends=('jre7' 'libx11')
provides=('java-environment=7')
conflicts=('java-environment')
backup=('etc/conf.d/derby-network-server'
        'etc/profile.d/jdk.csh'
        'etc/profile.d/jdk.sh')
install=jdk.install
source=("http://download.oracle.com/otn-pub/java/jdk/$pkgver-$_build/jdk-$pkgver-linux-${_arch}.tar.gz"
        'java-monitoring-and-management-console.desktop'
        'java-visualvm.desktop'
        'jdk.sh'
        'jdk.csh'
        'derby-network-server'
        'derby-network-server.conf'
        'javaws-launcher')
md5sums=(''
         'da10de5e6507c392fc9871076ef53ec6'
         '35fd89c5c170021d2183593335703703'
         'b83ab5742651b4234b7d2e20785c6693'
         '6f4cbf332816d2c4e9578ecd1d0dce7f'
         'a279e195e249000646895d93e199860d'
         '4bdff6982c66d24a879c424aaac3d04d'
         '45c15a6b4767288f2f745598455ea2bf')

 [ "$CARCH" == 'i686' ]   && md5sums[0]='f66c309ab38d6ba6651f7d98cd58d9d5'
 [ "$CARCH" == 'x86_64' ] && md5sums[0]='372b9dcde93230522672837e1820f939'
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -o %o %u --header "Cookie:oraclelicensejdk-${pkgver}-oth-JPR=accept-securebackup-cookie;gpw_e24=http://edelivery.oracle.com"')

package(){
  msg2 "Creating required dirs"
  cd $(ls -1d jdk1.7.0_* | tail -1)
  install -d "$pkgdir"/{opt/java/{,jre/lib/$_arch2},etc/profile.d,usr/share/{applications,licenses/jdk}}

  msg2 "Fetching missing libraries from 'jre' to fix jconsole"
  mv jre/lib/$_arch2/lib{saproc,attach}.so "$pkgdir"/opt/java/jre/lib/$_arch2/

  msg2 "Removing already provided 'jre' and redundant .bat scripts"
  rm -r jre/ # lib/visualvm/platform/docs/ # lib/desktop/
  find . -name '*\.bat' -delete

  msg2 "Moving stuff in place"
  mv * "$pkgdir"/opt/java/

  msg2 "Installing scripts, .desktop files, confs and licenses"
  cd "$srcdir"
  install -m755 jdk.{c,}sh "$pkgdir"/etc/profile.d/
  cp "$pkgdir"/opt/java/{COPYRIGHT,LICENSE,THIRDPARTYLICENSEREADME.txt} "$pkgdir"/usr/share/licenses/jdk/
  cp java-{visualvm,monitoring-and-management-console}.desktop "$pkgdir"/usr/share/applications/
  install -D derby-network-server "$pkgdir"/etc/rc.d/derby-network-server
  install -Dm644 derby-network-server.conf "$pkgdir"/etc/conf.d/derby-network-server
}
