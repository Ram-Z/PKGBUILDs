# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributer: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)
# Contributer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: solsTiCe d'Hiver <solstice.dhiver@gmail.com>

pkgname=lesspipe
pkgver=1.72
pkgrel=1
pkgdesc='an input filter for the pager less'
url="http://www-zeuthen.desy.de/~friebel/unix/lesspipe.html"
depends=('less')
arch=('any')
license=('GPL')
install="lesspipe.install"
optdepends=('perl'
	    'rpmextract: support for rpm files'
	    'fastjar: support for jar files'
	    'unzip: support for zip files'
	    'unrar: support for rar files'
	    'p7zip: support for 7za files'
	    'cabextract: support for cab files'
	    'cdrkit: support for iso files'
	    'html2text: support for html files'
	    'antiword: support for word file'
	    'unrtf: support for rtf file'
	    'imagemagick: support for some image file')
source=(http://downloads.sourceforge.net/project/lesspipe/lesspipe/$pkgver/lesspipe-$pkgver.tar.gz
	lesspipe.sh)
md5sums=('0fdb9d4ab5dd570806e778b6815ea276'
         '675b56f560c600f43108adfd98222251')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --yes
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  mkdir -p $pkgdir/usr/share/man/man1
  make install PREFIX=$pkgdir/usr
  install -D $srcdir/lesspipe.sh $pkgdir/etc/profile.d/lesspipe.sh
}
