# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libuv
pkgver=1.6.0
pkgrel=1
pkgdesc="Multi-platform support library with a focus on asynchronous I/O"
arch=('i686' 'x86_64')
url="https://github.com/libuv/libuv"
license=('custom')
depends=('glibc')
makedepends=('python3-sphinx')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libuv/libuv/archive/v$pkgver.tar.gz")
sha256sums=('1d60d79b942f5a4e53b7eddd96faccf77257ee5d3ae9865cbe9e719f6e164fea')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
  make man -C docs
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 AUTHORS \
    "$pkgdir"/usr/share/doc/$pkgname/AUTHORS
  install -Dm644 README.md \
    "$pkgdir"/usr/share/doc/$pkgname/README.md
  install -Dm644 ChangeLog \
    "$pkgdir"/usr/share/doc/$pkgname/ChangeLog
  install -Dm644 docs/build/man/libuv.1 \
    "$pkgdir"/usr/share/man/man1/libuv.1
}

