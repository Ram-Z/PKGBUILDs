# CCR: Ben R <thebenj88 *AT* gmail *DOT* com>

pkgname=libwebkit-gtk2
pkgver=1.10.2
pkgrel=1
pkgdesc="An opensource web content engine"
arch=('x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3' 'icu' 'gstreamer0.10-base' 'libsoup' 'enchant')
makedepends=('gperf' 'gobject-introspection' 'python2' 'gtk2' 'ruby' 'filesystem-extra')
provides=('libwebkit')
options=('!libtool' '!emptydirs')
install=libwebkit.install
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz)
sha256sums=('19234f8e4edfaefe91ed06471a2aa8c71d26dd55d33787eede1dcfc9a3a96d9c')

build() {

  cd "${srcdir}/webkitgtk-${pkgver}"

  # Suppress a *lot* of gcc 4.8 warnings
  CPPFLAGS+=" -Wno-unused-local-typedefs"

  # fix a bug, where documentation is build even when gtk-doc is not installed, which fails
  # http://www.linuxfromscratch.org/blfs/view/svn/x/webkitgtk.html
  sed -i '/generate-gtkdoc --rebase/s:^:# :' GNUmakefile.in


  PYTHON=/usr/bin/python2 ./configure --prefix=/extra/usr \
    --enable-introspection \
    --disable-geolocation \
    --with-font-backend=freetype --disable-gtk-doc \
    --with-unicode-backend=icu \
    --with-gtk=2.0 \
    --disable-webkit2 \
    --enable-spellcheck "$@"


  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make all stamp-po
}

package() {
  cd "$srcdir/webkitgtk-$pkgver"
  make -j1 DESTDIR="${pkgdir}" install
}
