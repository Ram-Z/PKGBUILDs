# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgbase=luasocket
pkgname=lua-socket
pkgver=2.0.2
pkgrel=1
pkgdesc='Networking support library for the Lua language'
arch=('x86_64')
url='http://www.cs.princeton.edu/~diego/professional/luasocket/'
license=('MIT')
depends=('lua')
source=("http://luaforge.net/frs/download.php/2664/luasocket-$pkgver.tar.gz"
        'luasocket-unixsocket.patch')
md5sums=('41445b138deb7bcfe97bff957503da8e'
         '8d116bb70bd7a2fa15cbe949d4cfc35e')

build() {
  cd luasocket-$pkgver

  patch -Np1 -i "$srcdir/luasocket-unixsocket.patch"
  make LUAINC=-I/usr/include/lua5.1
}

package() {
  cd luasocket-$pkgver

  make DESTDIR="$pkgdir/" \
    INSTALL_TOP_SHARE='$(DESTDIR)/usr/share/lua/5.1' \
    INSTALL_TOP_LIB='$(DESTDIR)/usr/lib/lua/5.1' install
  install -D -m0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
