# Maintainer:  Samir Benmendil <samir.benmendil@gmail.com>
# Contributor: Daniele Cocca <jmc@chakra-project.org>

pkgname=mediainfo
pkgver=0.7.71
pkgrel=1
pkgdesc="Supplies technical and tag information about a video or audio file"
arch=('x86_64')
url="http://mediainfo.sourceforge.net"
license=('GPL')
depends=('libmediainfo')
makedepends=('libtool' 'automake' 'autoconf')
source=("http://downloads.sourceforge.net/mediainfo/mediainfo_${pkgver}.tar.bz2")
md5sums=('94f5960dfe66d5d051223de021597476')

build() {
  cd "${srcdir}/MediaInfo/Project/GNU/CLI"

  sh ./autogen
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/MediaInfo/Project/GNU/CLI"

  make install DESTDIR="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
