# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgbase=mplayer
pkgname=('mencoder')
pkgver=35624
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Free command line video decoding, encoding and filtering tool"
depends=('enca' 'a52dec' 'libvpx' 'x264' 'libmng' 'libdca' 'bzip2' 'lame' 'giflib' 'libpng' 'xvidcore' 'libmad' 'libtheora' 
    'fribidi' 'libjpeg' 'faac' 'faad2' 'schroedinger' 'mpg123' 'libass' 'libvorbis')
makedepends=('libvdpau' 'unzip' 'yasm')
license=('GPL')
url="http://www.mplayerhq.hu/"
options=(!buildflags !emptydirs)
source=("http://chakra-linux.org/sources/${pkgbase}/${pkgbase}-${pkgver}.tar.xz")
md5sums=('99c1fe331a101f4b137226edb6836b77')

build() {
    cd "$srcdir/$pkgbase"

    ./configure --prefix=/usr \
        --disable-mplayer \
        --enable-runtime-cpudetection \
        --disable-gui \
        --disable-arts \
        --disable-liblzo \
        --disable-speex \
        --disable-openal \
        --disable-libdv \
        --disable-musepack \
        --disable-esd \
        --disable-mga \
        --disable-ass-internal \
        --disable-cdparanoia \
        --confdir=/etc/mplayer
    [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

    make mencoder
}

package() {
    cd "$srcdir/$pkgbase"
    make DESTDIR="$pkgdir" install-mencoder install-mencoder-man
    find "$pkgdir/usr/share/man" -name 'mplayer.1' -exec bash -c 'mv "$1" "${1/mplayer/mencoder}"' _ {} \;
}
