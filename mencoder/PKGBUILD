# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgbase=mplayer
pkgname=('mencoder')
pkgver=35421
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Free command line video decoding, encoding and filtering tool"
depends=('enca' 'a52dec' 'libvpx' 'x264' 'libmng' 'libdca' 'bzip2' 'lame' 'alsa-lib' 'fontconfig' 'giflib' 'libpng' 
    'xvidcore' 'opencore-amr' 'libmad' 'libtheora' 'fribidi' 'libjpeg' 'faac' 'faad2' 'schroedinger' 'mpg123' 'libass'
    'libbluray' 'libcdio' 'libvorbis')
makedepends=('libxxf86dga' 'libxxf86vm' 'libmad' 'libxinerama' 'sdl' 'lame' 'libtheora' 'xvidcore' 'libmng' 'libxss'
    'libgl' 'smbclient' 'aalib' 'jack' 'libcaca' 'x264' 'faac' 'faad2' 'lirc-utils'  'libxvmc' 'enca' 'libvdpau'
    'opencore-amr' 'libdca' 'a52dec' 'schroedinger' 'libvpx' 'libpulse' 'fribidi' 'unzip' 'mesa' 'live-media' 'yasm' 'git' 
    'fontconfig' 'mpg123' 'ladspa' 'libass' 'libbluray' 'libcdio' 'subversion')
license=('GPL')
url="http://www.mplayerhq.hu/"
options=(!buildflags !emptydirs)
source=(ftp://ftp.archlinux.org/other/$pkgbase/$pkgbase-$pkgver.tar.xz)
md5sums=('b5d8c24fc106b4ed58acda3b86c468fa')

build() {
    cd "$srcdir/$pkgbase"

    ./configure --prefix=/usr \
        --enable-runtime-cpudetection \
        --disable-gui \
        --disable-arts \
        --disable-liblzo \
        --disable-speex \
        --disable-openal \
        --disable-libdv \
        --disable-musepack \
        --disable-esd \
        --disable-mga \
        --disable-ass-internal \
        --disable-cdparanoia \
        --enable-xvmc \
        --enable-radio \
        --enable-radio-capture \
        --language=all \
        --confdir=/etc/mplayer
    [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

    make mencoder
}

package() {
    cd "$srcdir/$pkgbase"
    make DESTDIR="$pkgdir" install-mencoder install-mencoder-man
    find "$pkgdir/usr/share/man" -name 'mplayer.1' -exec bash -c 'mv "$1" "${1/mplayer/mencoder}"' _ {} \;
}
