# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Alexandre Petitjean <alpetitjean at gmail dot com>

pkgname=mopidy
pkgver=2.1.0
pkgrel=1
pkgdesc="An extensible music server written in Python"
arch=('any')
url="http://www.mopidy.com"
license=('APACHE')
depends=('python2'
         'python2-pykka>=1.1'
         'python2-setuptools'
         'gstreamer'
         'gst-python'
         'gst-plugins-good'
         'gst-plugins-ugly'
         'python2-tornado>=2.3')
makedepends=('python2-sphinx')
backup=('etc/mopidy/mopidy.conf')
install=$pkgname.install
source=("https://github.com/mopidy/mopidy/archive/v${pkgver}.tar.gz"
        'user-mopidy.service'
        'mopidy.conf'
        'logging.conf')
md5sums=('2a4229dfd9c21b561af0e8be3f0b15b8'
         '5b51b01d161f0db8928f87b37b1d62b3'
         '5e3c3308a75241b2f7ea5647287ca70c'
         'dbacd3800f63ef3c2a7135a6f3ee573d')

build() {
  cd "${pkgname}-${pkgver}"
  make -C docs SPHINXBUILD=sphinx-build2 man
  make -C docs SPHINXBUILD=sphinx-build2 html
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  install -Dm755 extra/mopidyctl/mopidyctl "${pkgdir}/usr/bin/mopidyctl"

  install -dm755 "${pkgdir}/usr/share/doc/mopidy"
  cp -r docs/_build/html "${pkgdir}/usr/share/doc/mopidy"

  install -dm755 "${pkgdir}/usr/share/man/man1/"
  gzip -c docs/_build/man/mopidy.1 > "${pkgdir}/usr/share/man/man1/mopidy.1.gz" 

  install -dm755 "${pkgdir}/usr/share/man/man8/"
  gzip -c extra/mopidyctl/mopidyctl.8 > "${pkgdir}/usr/share/man/man8/mopidyctl.8.gz" 

  install -Dm644 "${srcdir}/mopidy.conf" "${pkgdir}/etc/mopidy/mopidy.conf"
  install -Dm644 "${srcdir}/logging.conf" "${pkgdir}/etc/mopidy/logging.conf"

  install -Dm644 "extra/systemd/mopidy.service" "${pkgdir}/usr/lib/systemd/system/mopidy.service"
  install -Dm644 "extra/desktop/mopidy.desktop" "${pkgdir}/usr/share/applications/mopidy.desktop"

  install -dm755 -g 46 -o 46 "${pkgdir}/var/cache/mopidy"
  install -dm755 -g 46 -o 46 "${pkgdir}/var/log/mopidy"
  install -dm755 -g 46 -o 46 "${pkgdir}/var/lib/mopidy"
  install -dm755 -g 46 -o 46 "${pkgdir}/var/lib/mopidy/local"
  install -dm755 -g 46 -o 46 "${pkgdir}/var/lib/mopidy/media"
  install -dm755 -g 46 -o 46 "${pkgdir}/var/lib/mopidy/playlists"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 "$srcdir/user-mopidy.service" "$pkgdir/usr/lib/systemd/user/mopidy.service"
}

# vim:set ts=2 sw=2 et:
