# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)

pkgname=mps-youtube-git
pkgver=0.2.6.r44.g3d86b5b
pkgrel=1
pkgdesc="Terminal based YouTube jukebox with playlist management"
url='https://github.com/np1/mps-youtube'
arch=('any')
license=('GPL3')
depends=('python3' 'python-pafy-git' 'python3-requests')
makedepends=('python3-setuptools')
optdepends=('mpv: Alternative to mplayer for playback'
            'mplayer: Alternative to mpv for playback')
conflicts=('mps-youtube')
install=mps-youtube-git.install
source=('git+https://github.com/np1/mps-youtube.git')

md5sums=('SKIP')

pkgver() {
  cd mps-youtube
  git describe --tags --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

package() {
  cd mps-youtube
  python3 setup.py install --root="$pkgdir" --optimize=1
}
