# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv
pkgver=0.3.10
pkgrel=1
pkgdesc="A movie player and encoder for linux, based on MPlayer/mplayer2"
arch=('x86_64')
license=('GPL')
url="http://mpv.io"
depends=('desktop-file-utils' 'ffmpeg' 'hicolor-icon-theme' 'lcms2'
         'libcdio-paranoia' 'libdvdnav' 'libdvdread' 'libgl' 'libquvi' 'libva'
         'libxinerama' 'libxkbcommon' 'libxv' 'lirc-utils' 'lua' 'mpg123'
         'portaudio' 'wayland' 'xdg-utils')
makedepends=('mesa' 'python2-docutils' 'ladspa')
options=(!emptydirs !buildflags)
install=mpv.install
source=("https://github.com/mpv-player/$pkgname/archive/v${pkgver}.tar.gz")
md5sums=('9cfd0a4118b260bff4a3bc21cac7d1ab')

prepare() {
  cd "$pkgname-$pkgver"
  ./bootstrap.py
}

build() {
  cd "$pkgname-$pkgver"

  ./waf configure \
      --prefix=/usr \
      --confdir=/etc/mpv
      # --disable-lircc \
      # --disable-joystick \
      # --disable-radio \
      # --disable-radio-capture \
      # --disable-radio-v4l2 \
      # --disable-rpath \
      # --disable-libbs2b \
      # --disable-direct3d \
      # --disable-corevideo \
      # --disable-cocoa \
      # --disable-coreaudio
  ./waf build
}

package() {
  cd "$pkgname-$pkgver"

  ./waf install --destdir="$pkgdir"

  install -d ${pkgdir}/usr/share/doc/mpv/examples

  # some extra useful stuff "make install" doesn't install
  install -m644 etc/{input,example}.conf \
    "${pkgdir}/usr/share/doc/mpv/examples"
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "${pkgdir}/usr/share/doc/mpv"
}
