# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv
pkgver=0.2.1
pkgrel=1
pkgdesc="A movie player and encoder for linux, based on MPlayer/mplayer2"
arch=('x86_64')
license=('GPL')
url="http://mpv.io"
depends=('portaudio' 'ffmpeg' 'lcms2' 'libxinerama' 'libdvdread' 'libquvi' 'mpg123' 'libxv' 'libxkbcommon' 'libva')
makedepends=('mesa' 'docutils')
options=(!emptydirs)
source=("https://github.com/mpv-player/$pkgname/archive/v${pkgver}.tar.gz")
md5sums=('4e7c8823354f97753f0cbe17c61ec211')

build() {
  # Custom CFLAGS break the mpv build
  unset CFLAGS
  unset LDFLAGS

  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
      --confdir=/etc/mpv \
      --disable-lircc \
      --disable-joystick \
      --disable-radio \
      --disable-radio-capture \
      --disable-radio-v4l2 \
      --disable-rpath \
      --disable-libbs2b \
      --disable-direct3d \
      --disable-corevideo \
      --disable-cocoa \
      --disable-coreaudio
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/share/doc/mpv/examples

  # some extra useful stuff "make install" doesn't install
  install -m644 ${srcdir}/$pkgname-$pkgver/etc/{input,example,encoding-example-profiles}.conf "${pkgdir}/usr/share/doc/mpv/examples"
  install -m644 ${srcdir}/$pkgname-$pkgver/DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"
  install -m755 ${srcdir}/$pkgname-$pkgver/TOOLS/mpv_identify.sh "${pkgdir}/usr/bin"
}
