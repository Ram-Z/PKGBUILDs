# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv
pkgver=0.7.2
pkgrel=1
pkgdesc="A movie player and encoder for linux, based on MPlayer/mplayer2"
arch=('x86_64')
license=('GPL')
url="http://mpv.io"
depends=(
  'ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libgl' 'enca' 'libguess'
  'libxinerama' 'mpg123' 'libxv' 'libxkbcommon' 'libva' 'wayland'
  'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua' 'libdvdnav'
  'libguess' 'portaudio' 'libxrandr'
)
makedepends=('mesa' 'python2-docutils' 'ladspa')
options=(!emptydirs !buildflags)
install=mpv.install
source=("https://github.com/mpv-player/$pkgname/archive/v${pkgver}.tar.gz")
md5sums=('5081934235bf1d531364f59e5f718024')

prepare() {
  cd "$pkgname-$pkgver"
  ./bootstrap.py
}

build() {
  cd "$pkgname-$pkgver"

  ./waf configure \
      --prefix=/usr \
      --confdir=/etc/mpv
      # --disable-lircc \
      # --disable-joystick \
      # --disable-radio \
      # --disable-radio-capture \
      # --disable-radio-v4l2 \
      # --disable-rpath \
      # --disable-libbs2b \
      # --disable-direct3d \
      # --disable-corevideo \
      # --disable-cocoa \
      # --disable-coreaudio
  ./waf build
}

package() {
  cd "$pkgname-$pkgver"

  ./waf install --destdir="$pkgdir"

  install -d ${pkgdir}/usr/share/doc/mpv/examples

  # some extra useful stuff "make install" doesn't install
  install -m755 TOOLS/umpv \
    "${pkgdir}"/usr/bin/umpv
  install -m644 etc/{input,example}.conf \
    "${pkgdir}/usr/share/doc/mpv/examples"
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "${pkgdir}/usr/share/doc/mpv"
}
