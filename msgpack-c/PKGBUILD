# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Anatol Pomozov
# Contributor: Leen Jewel <leenjewel@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=1.1.0
_tag=cpp-$pkgver
pkgrel=1
pkgdesc='An efficient object serialization library'
arch=(i686 x86_64)
url='http://msgpack.org/'
license=("Apache")
depends=("glibc")
checkdepends=("gtest")
source=($pkgname-$pkgver.zip::https://github.com/msgpack/msgpack-c/archive/$_tag.zip)
sha1sums=('5e2d92e1c63b77c526416d72bd2364bd8ec708d7')

build() {
  cd msgpack-c-$_tag 
  ./bootstrap
  ./configure --prefix=/usr
  make
}

check() {
  cd msgpack-c-$_tag
  make check  
}

package() {
  cd msgpack-c-$_tag
  make DESTDIR="$pkgdir" install
}
