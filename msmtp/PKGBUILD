# Maintainer: Ram-Z <ram-z@chakra-project.org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=msmtp
pkgver=1.4.31
pkgrel=1
arch=('x86_64')
license=('GPL3')
pkgdesc="A mini smtp client"
url="http://${pkgname}.sourceforge.net"
depends=('gsasl')
#makedepends=('texlive-latexextra')
install=msmtp.install
source=("http://download.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('c0edce1e1951968853f15209c8509699ff9e9ab5')

prepare() {
  cd ${pkgbase}-${pkgver}
  sed -i 's|/usr/bin/python|/usr/bin/python2|' scripts/${pkgname}-gnome-tool/${pkgname}-gnome-tool.py
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-ssl=gnutls
  make
  #make -C doc html pdf
}

package() {
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install 
  #make DESTDIR="${pkgdir}" -C doc install-html install-pdf

# Installing example configs and scripts to /usr/share/doc/${pkgname}
# as they are not installed by default (Debian and Gentoo do it this way)
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r scripts/{find_alias,${pkgname}-gnome-tool,${pkgname}queue,${pkgname}q,set_sendmail} "${pkgdir}/usr/share/doc/${pkgname}/"
  install -D -m644 doc/*.example "${pkgdir}/usr/share/doc/${pkgname}/"

  install -D -m644 scripts/vim/${pkgname}.vim "${pkgdir}/usr/share/vim/vimfiles/syntax/${pkgname}.vim"
}

