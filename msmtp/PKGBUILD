# Maintainer: Ram-Z <ram-z@chakra-project.org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=msmtp
pkgver=1.6.6
pkgrel=1
arch=('x86_64')
license=('GPL3')
pkgdesc="A mini smtp client"
url="http://${pkgname}.sourceforge.net"
depends=('libgsasl')
makedepends=('texlive-latexextra')
install=msmtp.install
source=("http://download.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('f997f40dfb3f882df837cfd9a63bd4d271dcdc41')

build() {
  cd ${pkgbase}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-ssl=gnutls
  make
  make -C doc html
}

package() {
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install 
  make DESTDIR="${pkgdir}" -C doc install-html

# Installing example configs and scripts to /usr/share/doc/${pkgname}
# as they are not installed by default (Debian and Gentoo do it this way)
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r scripts/{find_alias,${pkgname}queue,${pkgname}q,set_sendmail} "${pkgdir}/usr/share/doc/${pkgname}/"
  install -D -m644 doc/*.example "${pkgdir}/usr/share/doc/${pkgname}/"

  install -D -m644 scripts/vim/${pkgname}.vim "${pkgdir}/usr/share/vim/vimfiles/syntax/${pkgname}.vim"
}

