# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Loui Chang <louipc dot ist at NOSPAM gmail dot com>

_pkgname=mutt-kz
pkgname=mutt-kz-git
pkgver=1.5.22.1.rc1.32.g738df8f
_muttver=1.5.22.1
pkgrel=1
pkgdesc="Mutt with notmuch support and another improvements."
url="https://github.com/karelzak/mutt-kz"
arch=(i686 x86_64)
license=('GPL')
depends=('gdbm' 'gpgme' 'openssl>=0.9.82' 'libidn' 'libsasl' 'mime-types' 'ncurses' 'notmuch' 'zlib')
makedepends=('git' 'gnupg' 'libxslt')
conflicts=('mutt')
provides=('mutt')
source=('git+https://github.com/karelzak/mutt-kz.git')
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd $_pkgname

  ./prepare \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gpgme \
    --enable-hcache \
    --enable-imap \
    --enable-notmuch \
    --enable-pgp \
    --enable-pop \
    --enable-smtp \
    --with-idn \
    --with-sasl \
    --with-ssl=/usr

  make
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/bin/{flea,muttbug}
  rm "$pkgdir"/usr/share/man/man1/{flea,muttbug}.1
  rm "$pkgdir"/etc/mime.types{,.dist}
  install -Dm644 contrib/gpg.rc "$pkgdir/etc/Muttrc.gpg.dist"
}
