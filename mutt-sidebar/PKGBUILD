# Maintainer: Ram-Z <ram-z[at]chakra-project[dot]org>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Gris Ge <cnfourt@gmail.com>

pkgname=mutt-sidebar
pkgver=1.5.22
pkgrel=1
pkgdesc="A small but very powerful text-based mail client with sidebar-patch"
arch=('x86_64')
url='http://www.mutt.org/'
license=('GPL')
depends=('openssl' 'gdbm' 'mime-types' 'libsasl' 'slang' 'gpgme')
conflicts=('mutt')
provides=('mutt')
source=("ftp://ftp.mutt.org/mutt/devel/mutt-${pkgver}.tar.gz"
        "ftp://ftp.openbsd.org/pub/OpenBSD/distfiles/mutt/sidebar-${pkgver}.diff1.gz"
        "ftp://ftp.openbsd.org/pub/OpenBSD/distfiles/mutt/trashfolder-${pkgver}.diff0.gz"
)
md5sums=('48267aba1bc53db636777f4a1ec87cb6'
         'f9eeabbd99b5c0ebfa659d0a30d8f2fa'
         '1e7d786112c2bb3cc50dfe743c6e0bec')

prepare() {
    rm -rf $srcdir/mutt-$pkgver
    tar xfv $srcdir/mutt-${pkgver}.tar.gz -C $srcdir/
    cd $srcdir/mutt-$pkgver

    patch -p1 -i "$srcdir"/trashfolder-${pkgver}.diff0
    patch -p1 -i "$srcdir"/sidebar-${pkgver}.diff1

    autoreconf -vfi
}

build() {
    cd $srcdir/mutt-$pkgver

    ./configure \
        ISPELL=/usr/bin/hunspell \
        SENDMAIL=/usr/sbin/sendmail \
        --enable-gpgme \
        --enable-hcache \
        --enable-imap \
        --enable-pop \
        --enable-smtp \
        --prefix=/usr \
        --sysconfdir=/etc \
        --with-idn \
        --with-regex \
        --with-ssl=/usr \
        --with-sasl \
        --with-slang

    make
}

package() {
    cd $srcdir/mutt-$pkgver
    make DESTDIR=$pkgdir install

    rm -f ${pkgdir}/usr/bin/{flea,muttbug}
    rm -f ${pkgdir}/usr/share/man/man1/{flea,muttbug}.1
    rm -f ${pkgdir}/etc/mime.types*
    install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
