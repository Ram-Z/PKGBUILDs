# Maintainer:  Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: Bart≈Çomiej Piotrowski
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

pkgname=ncmpcpp
pkgver=0.7.5
pkgrel=1
pkgdesc='Almost exact clone of ncmpc with some new features'
arch=('i686' 'x86_64')
url='http://ncmpcpp.rybczak.net/'
license=('GPL')
depends=('curl' 'libmpdclient' 'taglib' 'ncurses' 'fftw' 'boost-libs')
source=(http://ncmpcpp.rybczak.net/stable/$pkgname-$pkgver.tar.bz2
        missing-include.patch)
md5sums=('2edae67445a925175e75c223edc95ce7'
         '14fa33349661c8e6c2a323970ef1e98c')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../missing-include.patch
}

build() {
  cd $pkgname-$pkgver
  BOOST_LIB_SUFFIX='' ./configure \
    --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-unicode \
    --enable-visualizer \
    --with-curl \
    --with-fftw \
    --with-taglib

  make
  make -C extras
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm755 extras/artist_to_albumartist $pkgdir/usr/bin/
}
