# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Thorsten Toepper <atsutane@freethoughts.de>

pkgname=newsbeuter-git
_pkgname="newsbeuter"
pkgver=1717.125afcd
pkgrel=1
pkgdesc='A RSS feed reader for the text console with special Podcast support.'
url='http://www.newsbeuter.org'
arch=('i686' 'x86_64')
license=('MIT')
depends=('sqlite3' 'curl' 'libxml2' 'stfl' 'json-c')
optdepends=('ruby')
makedepends=('git' 'swig' 'asciidoc' 'docbook-xsl' 'pkg-config')
options=('docs' '!libtool')
conflicts=('newsbeuter')
source=('git://github.com/akrennmair/newsbeuter.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$_pkgname"
  ./config.sh
  sed -i 's+/usr/local+/usr+' Makefile
  make
  make doc
}

package() {
  cd "$_pkgname"

  make DESTDIR=${pkgdir} install
  install -Dm644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
