# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Olivier Ramonat <olivier at ramonat dot fr>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Bryan Ames <b130610@gmail.com>

_pkgname=notmuch
pkgname=notmuch-git
pkgver=0.18.1.51.gbbbdf04
pkgrel=1
pkgdesc="Notmuch is not much of an email program"
arch=(i686 x86_64)
url="http://notmuchmail.org/"
license=('GPL3')
provides=('notmuch' 'notmuch-vim' 'notmuch-runtime')
conflicts=('notmuch' 'notmuch-vim' 'notmuch-runtime')
depends=('xapian-core' 'gmime' 'talloc')
makedepends=('python2' 'python3' 'emacs' 'gnupg' 'ruby' 'pkg-config')
optdepends=('emacs: for using the emacs interface'
            'vim: for using the vim interface'
            'python2: for using the python2 bindings'
            'python3: for using the python3 bindings'
            'ruby: for using the ruby bindings'
            'gnupg: for email encryption')
source=("git://notmuchmail.org/git/notmuch")
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long | sed -e 's/-/./g'
}

prepare() {
  cp -r $_pkgname/bindings/python{,3}

  # prepare python3 bindings
  cd $srcdir/$_pkgname/bindings/python3
  find "." -name '*.py' -print0 |xargs -0 \
      sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python3,' \
             -e 's,^#!/usr/bin/python$,#!/usr/bin/python3,'
  ln -s ../../lib/libnotmuch.so.1 libnotmuch.so.1

  # prepare python2 bindings
  cd $srcdir/$_pkgname/bindings/python
  find "." -name '*.py' -print0 |xargs -0 \
      sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
             -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
  ln -s ../../lib/libnotmuch.so.1 libnotmuch.so.1

  sed -i -e 's/\(^.*\)python\(.*\.py$\)/\1python2\2/' ../../doc/Makefile.local
}

build() {
  cd "$srcdir/$_pkgname"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --zshcompletiondir=/usr/share/zsh/site-functions
  make

  cd $srcdir/$_pkgname/bindings/ruby
  ruby extconf.rb --vendor
  make

  cd $srcdir/$_pkgname/bindings/python3
  env LD_LIBRARY_PATH="." python3 setup.py build

  cd $srcdir/$_pkgname/bindings/python
  env LD_LIBRARY_PATH="." python2 setup.py build
}

# check() {
#   cd "$srcdir/$_pkgname"
#   make test
# }

package(){
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" LIBDIR_IN_LDCONFIG=0 install

  install -D notmuch $pkgdir/usr/sbin/notmuch

  mkdir -p $pkgdir/usr/share/vim/vimfiles/{plugin,syntax}

  cd vim && make prefix="$pkgdir/usr/share/vim/vimfiles" install

# Install ruby bindings
  cd $srcdir/$_pkgname/bindings/ruby
  sed -i -e 's,/site_ruby,,g' Makefile
  make exec_prefix=$pkgdir/usr prefix=$pkgdir/usr install

  # Install python bindings
  cd $srcdir/$_pkgname/bindings/python
  env LD_LIBRARY_PATH="." python2 setup.py install --prefix=/usr --root=$pkgdir

  cd $srcdir/$_pkgname/bindings/python3
  env LD_LIBRARY_PATH="." python3 setup.py install --prefix=/usr --root=$pkgdir
}
