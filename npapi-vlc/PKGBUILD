# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>

pkgname=npapi-vlc
pkgver=2.2.2
pkgrel=1
pkgdesc="NPAPI plugin for libvlc"
arch=('i686' 'x86_64')
url="https://code.videolan.org/videolan/npapi-vlc.git"
license=('LGPL-2.1')
options=('!libtool')
depends=('vlc')
makedepends=('npapi-sdk' 'git')
source=("git+https://code.videolan.org/videolan/npapi-vlc.git#tag=$pkgver"
        "git+https://code.videolan.org/videolan/libvlcpp.git")
md5sums=('SKIP'
         'SKIP')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.vlcpp.url "$srcdir"/libvlcpp
  git submodule update
}

build() {
  cd $pkgname

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
