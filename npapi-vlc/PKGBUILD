# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>

pkgname=npapi-vlc
pkgver=2.2.0
pkgrel=1
pkgdesc="NPAPI plugin for libvlc"
arch=('i686' 'x86_64')
url="http://git.videolan.org/?p=$pkgname.git;a=summary"
license=('LGPL-2.1')
options=('!libtool')
depends=('vlc')
makedepends=('npapi-sdk' 'git')
# This packages uses git because there is no official tarball,
# but it still builds $pkgver by fetching the appropiate tag.
source=("${pkgname}::git://git.videolan.org/${pkgname}.git#tag=${pkgver}")
md5sums=('SKIP')


build() {
  cd "$pkgname"

  ./autogen.sh
  ./configure --prefix=/usr
  msg "Starting make..."
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
}
