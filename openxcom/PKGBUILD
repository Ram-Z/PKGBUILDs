# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: TheBenj88 <thebenj88 *AT* gmail *DOT* com>

pkgname=openxcom
pkgver=0.9
pkgrel=1
pkgdesc="An open-source reimplementation of the famous X-COM game"
arch=('x86_64')
url="http://openxcom.org/"
license=('GPL3')
depends=('sdl' 'gcc-libs' 'sdl_gfx' 'sdl_mixer' 'sdl_image' 'timidity++' 'yaml-cpp0.3' 'mesa' 'glu')
install="${pkgname}.install"
source=("openxcom.tar.gz::https://github.com/SupSuper/OpenXcom/tarball/v${pkgver}"
        "${pkgname}.install")
sha256sums=('340ff7292baa9a2b7ce4b244cc6dc0200c1d3b807a59087ef9827bba5c135634'
            '102150a0124ffe1fe1ab11f05d2e7a8c8e20decba9cef78fa831fc2429cc8e46')

build() {
  cd "${srcdir}/SupSuper-OpenXcom-5c8b540/src"
  mv Makefile.simple Makefile
  # Fix a problem with yaml-cpp and pkg-config
  sed -i "s_--cflags sdl yaml-cpp\`_--cflags sdl\` -I/usr/include/yaml-cpp_" Makefile
  # Fix a compilation error
  sed -i "s_?=_+=_" Makefile

  make || return 1
}

package() {
  cd "${srcdir}/SupSuper-OpenXcom-5c8b540"

  # Install binary
  install -Dm755 "bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install data files
  mkdir -p "${pkgdir}/usr/share/${pkgname}/data/"{Language,SoldierName,Shaders,Ruleset}
  install -m644 bin/data/Language/* "${pkgdir}/usr/share/${pkgname}/data/Language"
  install -m644 bin/data/SoldierName/* "${pkgdir}/usr/share/${pkgname}/data/SoldierName"
  install -m644 bin/data/Shaders/* "${pkgdir}/usr/share/${pkgname}/data/Shaders"
  install -m644 bin/data/Ruleset/* "${pkgdir}/usr/share/${pkgname}/data/Ruleset"
  install -m644 bin/data/README.txt "${pkgdir}/usr/share/${pkgname}"

  # Install readme
  install -Dm644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
}
