# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Simon Zimmermann <simon@insmo.com>

pkgname=pass-git
pkgver=1.6.5.47.g0b2f803
pkgrel=1
pkgdesc='stores, retrieves, generates, and synchronizes passwords securely'
url=http://zx2c4.com/projects/password-store/
license='GPL2'
arch=('any')
makedepends=('git')
provides=(pass)
conflicts=(pass)
depends=('xclip' 'bash' 'git' 'gnupg2' 'pwgen' 'tree')
source=("${pkgname}::git://git.zx2c4.com/password-store")
md5sums=("SKIP")

pkgver() {
   cd $pkgname
   git describe --always | sed 's|-|.|g'
}

package() {
    cd ${srcdir}/${pkgname}
    make FORCE_ALL=1 DESTDIR="${pkgdir}" install

    cd contrib/dmenu
    install -Dm0755 passmenu "${pkgdir}"/usr/bin/passmenu
}
