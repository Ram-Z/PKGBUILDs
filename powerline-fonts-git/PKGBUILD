# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Devin Christensen <quixoten at gmail dot com>

pkgname=powerline-fonts-git
pkgver=r100.a44abd0
pkgrel=1
pkgdesc="Powerline fonts for X11 and the console"
arch=('any')
url=('https://github.com/Lokaltog/powerline-fonts')
license=('CPL')
depends=('fontconfig' 'xorg-font-utils')
makedepends=('git')
conflicts=('powerline-fonts')
install=$pkgname.install
source=('git://github.com/powerline/fonts.git')
md5sums=('SKIP')

pkgver() {
  cd fonts
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd fonts
  find . -iname '*.ttf' -execdir install -Dm644 {} "$pkgdir"/usr/share/fonts/TTF/{} \;
  find . -iname '*.otf' -execdir install -Dm644 {} "$pkgdir"/usr/share/fonts/OTF/{} \;
  find . -iname '*.pcf.gz' -execdir install -Dm644 {} "$pkgdir"/usr/share/fonts/misc/{} \;
  find . -iname '*.pcf.gz' -execdir install -Dm644 {} "$pkgdir"/usr/share/kbd/consolefonts/{} \;
}
