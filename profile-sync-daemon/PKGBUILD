# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname='profile-sync-daemon'
_pkgname_short='psd'
pkgver=3.0
pkgrel=3
pkgdesc='Symlinks and syncs browser profile dirs to RAM thus reducing HDD/SDD calls and speeding-up browsers.'
arch=('any')
url='https://wiki.archlinux.org/index.php/Profile-sync-daemon'
license=('GPL')
depends=('rsync')
optdepends=('cron: allow hourly (default) or periodic syncs of tmpfs <--> HDD/SDD storage')
conflicts=('firefox-sync' 'firefox-tmpfs-daemon' 'chromium-tmpfs' 'tmpfs-store')
replaces=('sync-browsers-daemon')
source=("http://repo-ck.com/source/$pkgname/$pkgname-$pkgver.tar.xz"
        "qupzilla.patch")
backup=("etc/$_pkgname_short.conf")
install=readme.install
sha256sums=('b58588c818dd9ed9a440e536fd8aaa587e1dd9bdfd2619bc85aa9ef6e3e61dfe'
            '2d64c1c0d1d090cd32946640ce57b808ddd15f56e1742feb7093736c80c4aa0d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 profile-sync-daemon < "${srcdir}/qupzilla.patch"
	gzip -f -9 $_pkgname_short.manpage
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
	install -Dm644 $_pkgname_short.conf "$pkgdir/etc/$_pkgname_short.conf"
	install -Dm755 rc.$_pkgname_short "$pkgdir/etc/rc.d/$_pkgname_short"
	install -Dm755 $_pkgname_short.cron.hourly "$pkgdir/etc/cron.hourly/$_pkgname_short-update"
	install -Dm644 "$_pkgname_short.service" "$pkgdir/lib/systemd/system/$_pkgname_short.service"
	
	install -g 0 -o 0 -Dm 0644 $_pkgname_short.manpage.gz "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
	install -g 0 -o 0 -Dm 0644 $_pkgname_short.manpage.gz "$pkgdir/usr/share/man/man1/$_pkgname_short.1.gz"
}

# vim: sts=2 ts=2 sw=2 et
