# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname='profile-sync-daemon'
pkgver=5.28
pkgrel=1
pkgdesc='Symlinks and syncs browser profiles to RAM (tmpfs) thus reducing HDD/SDD calls and speeding-up browsers.'
arch=('any')
url='https://github.com/graysky2/profile-sync-daemon'
license=('MIT')
depends=('rsync')
optdepends=('cron: allow hourly (default) or periodic syncs of tmpfs <--> HDD/SDD storage')
conflicts=('firefox-sync' 'firefox-tmpfs-daemon' 'chromium-tmpfs' 'tmpfs-store' 'goanysync' 'goanysync-git'
           'tmpfs-sync' 'user-profile-sync-daemon')
replaces=('sync-browsers-daemon')
source=("http://repo-ck.com/source/$pkgname/$pkgname-$pkgver.tar.xz")
backup=('etc/psd.conf')
install=psd.install
sha256sums=('559c86577cc1cec3032c47bb1948e4bb9df1be3ef036a2ff12e88b2a1236d6bc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ln -sf Makefile.archlinux Makefile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
  # allow pacman to implicately track /etc/psd.conf to inform users of changes
  mv "$pkgdir/etc/psd.conf" "$srcdir/$pkgname-$pkgver"
  install -Dm644 psd.conf "$pkgdir/etc/psd.conf"
}

# vim: sts=2 ts=2 sw=2 et
