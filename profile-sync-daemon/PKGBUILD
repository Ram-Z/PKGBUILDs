# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr)
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname='profile-sync-daemon'
pkgver=5.06
pkgrel=2
pkgdesc='Symlinks and syncs browser profile dirs to RAM thus reducing HDD/SDD calls and speeding-up browsers.'
arch=('any')
url='https://wiki.archlinux.org/index.php/Profile-sync-daemon'
license=('GPL')
depends=('rsync' 'gawk') # gawk is needed for the temp workaround in $pkgname.install
optdepends=('cron: allow hourly (default) or periodic syncs of tmpfs <--> HDD/SDD storage')
conflicts=('firefox-sync' 'firefox-tmpfs-daemon' 'chromium-tmpfs' 'tmpfs-store')
replaces=('sync-browsers-daemon')
source=("http://repo-ck.com/source/$pkgname/$pkgname-$pkgver.tar.xz")
backup=("etc/psd.conf")
install=psd.install
sha256sums=('f451af448333ac33053a8f7d5c06c1f898b3a095bdc9fca1af22363ca3f230ca')

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 psd.conf "$pkgdir/etc/psd.conf"
  install -Dm755 psd.cron.hourly "$pkgdir/etc/cron.hourly/psd-update"
  install -Dm644 "psd.service" "$pkgdir/usr/lib/systemd/system/psd.service"
  ln -s "$pkgname" "$pkgdir/usr/bin/psd"

  gzip -9 psd.manpage
  install -Dm 0644 psd.manpage.gz "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  ln -s "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/psd.1.gz"
}

# vim: sts=2 ts=2 sw=2 et
