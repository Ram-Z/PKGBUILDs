# $Id$
# Maintainer: SÃ©bastien "Seblu" Luttringer

pkgbase=python-msgpack
pkgname=('python3-msgpack' 'python2-msgpack')
pkgver=0.4.6
pkgrel=1
arch=('i686' 'x86_64')
url='https://github.com/msgpack/msgpack-python'
license=('Apache')
makedepends=('python2-cython' 'python3-cython' 'python3-setuptools' 'python2-setuptools')
checkdepends=('python3-pytest' 'python2-pytest' 'python3-six' 'python2-six')
source=("http://pypi.python.org/packages/source/m/msgpack-python/msgpack-python-$pkgver.tar.gz")
md5sums=('8b317669314cf1bc881716cccdaccb30')

build() {
  cd msgpack-python-$pkgver
  python2 setup.py build --build-lib=build/python2
  python3 setup.py build --build-lib=build/python
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

#check() {
#  cd msgpack-python-$pkgver
#  msg2 'python'
#  PYTHONPATH=$PWD/build/python py.test test
#  msg2 'python2'
#  PYTHONPATH=$PWD/build/python2 py.test2 test
#}

package_python3-msgpack() {
  pkgdesc='MessagePack serializer implementation for Python'
  depends=('python')

  cd msgpack-python-$pkgver
  python3 setup.py build --build-lib=build/python \
                  install --root="$pkgdir" --optimize=1
}

package_python2-msgpack() {
  pkgdesc='MessagePack serializer implementation for Python2'
  depends=('python2')

  cd msgpack-python-$pkgver
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
