# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgbase=python-munkres
pkgname=('python3-munkres' 'python2-munkres')
pkgver=1.0.12
pkgrel=1
pkgdesc='Munkres algorithm for Python'
arch=('any')
url='https://github.com/bmc/munkres'
license=('Apache')
makedepends=('python3-setuptools' 'python2-setuptools')
source=("python-munkres-${pkgver}.tar.gz::https://github.com/bmc/munkres/archive/release-${pkgver}.tar.gz")
sha256sums=('70b3b32b4fed3b354e5c42e4d1273880a33a13ab8c108a4247140eb661767a0b')

prepare() {
  mv {,python3-}munkres-release-${pkgver}
  cp -r python{3,2}-munkres-release-${pkgver}

  cd python3-munkres-release-${pkgver}

  sed 's|#!.*python$|#!/usr/bin/env python3|' -i $(grep -rl '#!.*python$')
}

build() {
  for py in python{3,2}; do
    pushd ${py}-munkres-release-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python3-munkres() {
  depends=('python3')

  cd python3-munkres-release-${pkgver}

  python3 setup.py install --root="${pkgdir}" --optimize='1'
}

package_python2-munkres() {
  depends=('python2')

  cd python2-munkres-release-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
