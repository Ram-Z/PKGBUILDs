# Maintainer:  Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: Florian Walch <florian+aur@fwalch.com>
# Contributor: Patrice Peterson <runiq at archlinux dot us>
# Contributor: lucc <l-m-h@web.de>
# Contributor: wenLiangcan <boxeed at gmail dot com>
# Contributor: astiam <astiamm at gmail dot com>

pkgbase=python-neovim
pkgname=(python2-neovim python3-neovim)
_realname=python-client
pkgver=0.1.4
pkgrel=1
arch=('any')
license=('Apache')
url='https://github.com/neovim/python-client'
makedepends=('python2-setuptools' 'python3-setuptools')
source=("https://github.com/neovim/python-client/archive/${pkgver}.tar.gz")
sha256sums=('dc4718ab8e239b60093d3f04bd41e9348c6b6b5850c8418e1f8fafc71c62eba5')

prepare() {
  cp -r $_realname-$pkgver{,-python2}
}

build() {
  cd $_realname-${pkgver}-python2
  python2 setup.py build

  cd $_realname-${pkgver}
  python3 setup.py build
}

package_python2-neovim() {
  pkgdesc='Adds support for Python 2 plugins to Neovim.'
  depends=('neovim' 'python2' 'python2-msgpack' 'python2-greenlet'
           'python2-trollius')
  optdepends=('python2-cairo: for experimental GUI (pynvim)'
              'python2-click: for experimental GUI (pynvim)'
              'python2-gobject: for experimental GUI (pynvim)')

  cd $_realname-${pkgver}-python2
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
}

package_python3-neovim() {
  pkgdesc='Adds support for Python 3 plugins to Neovim.'
  depends=('neovim' 'python3' 'python3-msgpack' 'python3-greenlet')
  provides=("python3-neovim=${pkgver}")
  conflicts=('python3-neovim')

  cd $_realname-${pkgver}
  python3 setup.py install --prefix=/usr --root="${pkgdir}"
}

# vim:set sw=2 sts=2 et:
