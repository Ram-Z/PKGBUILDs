# Maintainer:  Ram-Z <ram-z[at]chakraos[dot]org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Atlanis <emallson@archlinux.us>

pkgbase=python-oauth2client
pkgname=('python2-oauth2client' 'python3-oauth2client')
pkgver=2.2.0
pkgrel=1
pkgdesc="A client library for OAuth 2.0"
arch=('any')
url="https://github.com/google/oauth2client"
license=('Apache')
makedepends=('python2-setuptools' 'python3-setuptools' 'python3-httplib2'
             'python3-pyasn1' 'python3-pyasn1-modules' 'python3-rsa'
             'python3-six' 'python2-httplib2' 'python2-pyasn1'
             'python2-pyasn1-modules' 'python2-rsa' 'python2-six')
source=("https://github.com/google/oauth2client/archive/v${pkgver}.tar.gz")
md5sums=('9bd7416807005bddf281915c65c9ebd2')

prepare() {
  cd "$srcdir"

  cp -a oauth2client-${pkgver} oauth2client-py2-${pkgver}
  cd oauth2client-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/oauth2client-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/oauth2client-${pkgver}
  python3 setup.py build
}

package_python2-oauth2client() {
  depends=('python2-httplib2' 'python2-pyasn1' 'python2-pyasn1-modules' 'python2-rsa' 'python2-six')
  optdepends=('python2-gflags: for oauth2client.tools.run function')

  cd "$srcdir"/oauth2client-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}

package_python3-oauth2client() {
  depends=('python3-httplib2' 'python3-pyasn1' 'python3-pyasn1-modules' 'python3-rsa' 'python3-six')

  cd "$srcdir"/oauth2client-${pkgver}

  python3 setup.py install --skip-build --root="$pkgdir" --optimize=1
}
