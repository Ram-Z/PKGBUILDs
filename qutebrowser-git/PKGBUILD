# Maintainer:  Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Florian Bruhin (The Compiler) <archlinux.org@the-compiler.org>

pkgname=qutebrowser-git
pkgver=r3628.48c8350
pkgrel=1
pkgdesc="A keyboard-driven, vim-like browser based on PyQt5 and QtWebKit"
arch=("any")
url="http://www.qutebrowser.org/"
license=('GPL')
depends=('python3>=3.4' 'python3-setuptools' 'python3-pyqt5>=5.2' 'qt5-base>=5.2'
         'qt5-webkit>=5.2' 'libxkbcommon' 'python3-pypeg2' 'python3-jinja'
         'python3-pygments' 'xdg-utils' 'desktop-file-utils')
makedepends=('asciidoc')
optdepends=(
  'python3-colorlog: colored logging output'
  'gst-libav: media playback'
  'gst-plugins-base: media playback'
  'gst-plugins-good: media playback'
  'gst-plugins-bad: media playback'
  'gst-plugins-ugly: media playback'
)
options=(!emptydirs)
conflicts=('qutebrowser')
provides=('qutebrowser')
source=('git+https://github.com/The-Compiler/qutebrowser.git'
        'qutebrowser-git.install')
sha256sums=('SKIP'
            '4a88871e91f894890ce96feb620e4174c686db12e4fa91b5a7306454dc96bf67')
install=qutebrowser-git.install

pkgver() {
  cd "$srcdir/qutebrowser"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/qutebrowser"
  python3 scripts/asciidoc2html.py
  a2x -f manpage doc/qutebrowser.1.asciidoc
  python3 setup.py build
}

package() {
  cd "$srcdir/qutebrowser"
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 doc/qutebrowser.1 "$pkgdir/usr/share/man/man1/qutebrowser.1"
  install -Dm755 qutebrowser.desktop \
    "$pkgdir/usr/share/applications/qutebrowser.desktop"
  install -Dm755 icons/qutebrowser-16x16.png \
    "$pkgdir/usr/share/icons/hicolor/16x16/apps/qutebrowser.png"
  install -Dm755 icons/qutebrowser-24x24.png \
    "$pkgdir/usr/share/icons/hicolor/24x24/apps/qutebrowser.png"
  install -Dm755 icons/qutebrowser-32x32.png \
    "$pkgdir/usr/share/icons/hicolor/32x32/apps/qutebrowser.png"
  install -Dm755 icons/qutebrowser-48x48.png \
    "$pkgdir/usr/share/icons/hicolor/48x48/apps/qutebrowser.png"
  install -Dm755 icons/qutebrowser-64x64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/qutebrowser.png"
  install -Dm755 icons/qutebrowser-96x96.png \
    "$pkgdir/usr/share/icons/hicolor/96x96/apps/qutebrowser.png"
  install -Dm755 icons/qutebrowser-128x128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/qutebrowser.png"
  install -Dm755 icons/qutebrowser-256x256.png \
    "$pkgdir/usr/share/icons/hicolor/256x256/apps/qutebrowser.png"
  install -Dm755 icons/qutebrowser-512x512.png \
    "$pkgdir/usr/share/icons/hicolor/512x512/apps/qutebrowser.png"
  install -Dm755 icons/qutebrowser.svg \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/qutebrowser.svg"
}
