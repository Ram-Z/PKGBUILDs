# Maintainer:  Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Florian Bruhin (The Compiler) <archlinux.org@the-compiler.org>

pkgname=qutebrowser-git
pkgver=r3832.46ca0e4
pkgrel=1
pkgdesc="A keyboard-driven, vim-like browser based on PyQt5 and QtWebKit"
arch=("any")
url="http://www.qutebrowser.org/"
license=('GPL')
depends=(
  'desktop-file-utils'
  'libxkbcommon'
  'python3-jinja'
  'python3-pygments'
  'python3-pypeg2'
  'python3-pyqt5>=5.2'
  'python3-setuptools'
  'python3-yaml'
  'python3>=3.4'
  'qt5-base>=5.2'
  'qt5-webkit>=5.2'
  'xdg-utils'
)
makedepends=('asciidoc')
optdepends=(
  'python3-colorlog: colored logging output'
  'gst-libav: media playback'
  'gst-plugins-base: media playback'
  'gst-plugins-good: media playback'
  'gst-plugins-bad: media playback'
  'gst-plugins-ugly: media playback'
)
options=(!emptydirs)
conflicts=('qutebrowser')
provides=('qutebrowser')
source=('git+https://github.com/The-Compiler/qutebrowser.git'
        'qutebrowser-git.install')
sha256sums=('SKIP'
            '4a88871e91f894890ce96feb620e4174c686db12e4fa91b5a7306454dc96bf67')
install=qutebrowser-git.install

pkgver() {
  cd "$srcdir"/qutebrowser
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir"/qutebrowser
  python3 scripts/asciidoc2html.py
  a2x -f manpage doc/qutebrowser.1.asciidoc
  python3 setup.py build
}

package() {
  cd "$srcdir"/qutebrowser
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 doc/qutebrowser.1 "$pkgdir"/usr/share/man/man1/qutebrowser.1
  install -Dm755 qutebrowser.desktop \
    "$pkgdir"/usr/share/applications/qutebrowser.desktop

  for px in 16 24 32 48 64 96 128 256 512; do
    install -Dm755 icons/qutebrowser-${px}x${px}.png \
      "$pkgdir"/usr/share/icons/hicolor/${px}x${px}/apps/qutebrowser.png
  done
  install -Dm755 icons/qutebrowser.svg \
    "$pkgdir"/usr/share/icons/hicolor/scalable/apps/qutebrowser.svg
}
