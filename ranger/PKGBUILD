# Maintainer: schuay <jakob.gruber@gmail.com>
# Previous maintainer: Roman Zimbelmann <romanz@lavabit.com>

pkgname=ranger
pkgver=1.8.1
pkgrel=2
pkgdesc='A simple, vim-like file manager'
arch=('any')
url='http://ranger.nongnu.org'
license=('GPL')
depends=('python3')
optdepends=(
    'atool: for previews of archives'
    'elinks: for previews of html pages'
    'ffmpegthumbnailer: for video previews'
    'highlight: for syntax highlighting of code'
    'libcaca: for ASCII-art image previews'
    'lynx: for previews of html pages'
    'mediainfo: for viewing information about media files'
    'perl-image-exiftool: for viewing information about media files'
    'poppler: for pdf previews'
    'python-chardet: in case of encoding detection problems'
    'sudo: to use the "run as root"-feature'
    'transmission-cli: for viewing bittorrent information'
    'w3m: for previews of images and html pages')
conflicts=('ranger-git')
source=("http://ranger.nongnu.org/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('1433f9f9958b104c97d4b23ab77a2ac37d3f98b826437b941052a55c01c721b4'
            'SKIP')
validpgpkeys=('1E9B36EC051FF6F7FFC969A7F08CE1E200FB5CDF') # huterich

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Replace "python" with the python binary you want ranger to use!
  # (python 2.6 and >=3.1 supported, below 2.6 will certainly not work.)
  python3 setup.py -q install --root="${pkgdir}" --optimize=1
}
