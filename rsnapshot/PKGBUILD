# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: Steven Stillaway <steve@stillaway.net>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Thorsten Tasch <tht@thorstentasch.de>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>

pkgname=rsnapshot
pkgver=1.3.1
pkgrel=4
pkgdesc="A remote filesystem snapshot utility"
arch=(any)
url="http://www.rsnapshot.org"
license=('GPL')
depends=('perl' 'rsync' 'openssh')
backup=('etc/rsnapshot.conf')
source=("http://www.rsnapshot.org/downloads/${pkgname}-${pkgver}.tar.gz"
        "0001-added-missing-back.patch")
md5sums=('588f92995dcf60a6ea6df8d94a017e7e'
         'a7075d9366eda5e699be86f7ffe8246e')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -p1 -i "$srcdir"/0001-added-missing-back.patch
}
build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  sed -i 's:/usr/bin/pod2man:/usr/bin/core_perl/pod2man:' Makefile
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/etc/rsnapshot.conf.default "${pkgdir}"/etc/rsnapshot.conf
}
