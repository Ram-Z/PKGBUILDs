# Maintainer: Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributer: Yochai Gal <yochaigal@gmail.com>

pkgname=supermeatboy
pkgver=20120607
_pkgver=06072012
pkgrel=1
pkgdesc="[Humble Bundle 4] Super Meat Boy is a tough as nails platformer where you play as an animated cube of meat."
arch=('i686' 'x86_64')
 [[ $CARCH == 'i686' ]]   && _arch='x86'
 [[ $CARCH == 'x86_64' ]] && _arch='amd64'
url="http://www.supermeatboy.com/"
screenshot="http://www.supermeatboy.com/_media/meatboy/cornerlogo.jpg"
license=('custom')
depends=('openal' 'sdl')
makedepends=('unzip' 'tar')
source=('supermeatboy.desktop'
        'supermeatboy.sh')
md5sums=('ceda8cdca2aaff63a9cae2ef7d7cf0d4'
         '818b03f82053ce6e0e996a780869e110')
PKGEXT='.pkg.tar'

_archive="supermeatboy-linux-${_pkgver}-bin"
_archive_md5="90a563ff103b4610b41af5fd2bccb21a"

_bundle=('humblebundle4' 'humblebundle5')

_humble() {
  if [[ -f "${_gamedir}/${_archive}" ]]; then
    ln -sf "${_gamedir}/${_archive}"
  fi

  if [ ! -f "${_archive}" ]; then
    for bundle in ${_bundle[@]}; do
      case ${bundle} in
        "humblebundle"*) _key="_humblebundle${bundle:12}key";;
        *) continue;;
      esac
      if [ -n "${!_key}" ]; then
        msg2 "Getting your unique ${pkgname} download location."
        _uri="$(curl -s "http://www.humblebundle.com/downloads?key=${!_key}" | grep "${_archive}" | cut -d "'" -f 10)"
        wget $_uri
        mv ${_archive}* ${_archive}
        break
      fi
    done
    if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
      error "Unable to find \"${_archive}\"." 
      plain "You should run one of these commands before installing this pkg."
      for bundle in ${_bundle[@]}; do
        plain "$ export _${bundle}key=<your-${bundle}-key>"
      done
      plain "$ export _gamedir=<dir/to/${_archive}>"
      exit 1
    fi
  fi
  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi
  unset _key _uri _archive_md5 _bundle
}

build() {
  cd "${srcdir}"
  _humble

  unzip -o "${_archive}" -d "${pkgname}" || true
}

package() {
  cd "${srcdir}/${pkgname}/data"

  install -d "${pkgdir}/opt/${pkgname}"
  cp -dpr --no-preserve=ownership Levels resources UserData "${pkgdir}/opt/${pkgname}"

  install -Dm644 buttonmap.cfg gameaudio.dat gamedata.dat locdb.txt README-linux.txt supermeatboy.png \
    "${pkgdir}/opt/${pkgname}"
  install -Dm755 "${_arch}/SuperMeatBoy-${_arch}" "${pkgdir}/opt/${pkgname}/SuperMeatBoy"

  install -Dm755 "${srcdir}/supermeatboy.sh" "${pkgdir}/usr/bin/supermeatboy"
  install -Dm644 "${srcdir}/supermeatboy.desktop" "${pkgdir}/usr/share/applications/supermeatboy.desktop"
}

# vim: sts=2 ts=2 sw=2 et
