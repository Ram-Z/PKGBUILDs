# Maintainer: Samir Benmendil <samir.benmendil@gmail.com>

pkgname=swordsandsoldiers
pkgver=20120325
pkgrel=1
pkgdesc="[Humble Bundle Android2] Three factions are fighting each other to gain the powers of the gods."
arch=('i686' 'x86_64')
 [ $CARCH = 'i686' ]   && _arch='i386'
 [ $CARCH = 'x86_64' ] && _arch='amd64'
url="http://www.swordsandsoldiers.com/"
license=('custom: commercial')
depends=('sdl')
options=('!strip')
PKGEXT=".pkg.tar"

_archive="${pkgname}-${pkgver}-${_arch}.tar.gz"
 [ $CARCH = 'i686' ]   && _archive_md5="52fe87a6fa6bb331eed057f0ebf73da5"
 [ $CARCH = 'x86_64' ] && _archive_md5="5f0c9789fa053cbf6bac021a338245bb"

_bundle=('humblebundleandroid2')

_humble() {
  if [[ -f "${_gamedir}/${_archive}" ]]; then
    ln -sf "${_gamedir}/${_archive}"
  fi

  if [ ! -f "${_archive}" ]; then
    for bundle in ${_bundle[@]}; do
      case ${bundle} in
        "humblebundle"*) _key="_humblebundle${bundle:12}key";;
        *) continue;;
      esac
      if [ -n "${!_key}" ]; then
        msg2 "Getting your unique ${pkgname} download location."
        _uri="$(curl -s "http://www.humblebundle.com/downloads?key=${!_key}" | grep "${_archive}" | cut -d "'" -f 10)"
        wget $_uri
        mv ${_archive}* ${_archive}
        break
      fi
    done
    if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
      error "Unable to find \"${_archive}\"." 
      plain "You should run one of these commands before installing this pkg."
      for bundle in ${_bundle[@]}; do
        plain "$ export _${bundle}key=<your-${bundle}-key>"
      done
      plain "$ export _gamedir=<dir/to/${_archive}>"
      exit 1
    fi
  fi
  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi
  unset _key _uri _archive_md5 _bundle
}

build() {
  cd "${srcdir}"
  _humble

  tar xzf "${_archive}" || true

  unset _archive
}

package() {
  cd "$srcdir/$pkgname"

  local _install_path="/opt/${pkgname}"
  ESCAPED_PATH=$(echo "${_install_path}" |  sed 's/\//\\\//g')

  sed 's/\$INSTALL_PATH\$/'${ESCAPED_PATH}'/' SwordsAndSoldiers.desktop.in > SwordsAndSoldiers.desktop
  sed 's/\$INSTALL_PATH\$/'${ESCAPED_PATH}'/' SwordsAndSoldiersSetup.desktop.in > SwordsAndSoldiersSetup.desktop
  sed 's/\$INSTALL_PATH\$/'${ESCAPED_PATH}'/' swordsandsoldiers.in > swordsandsoldiers
  sed 's/\$INSTALL_PATH\$/'${ESCAPED_PATH}'/' swordsandsoldierssetup.in > swordsandsoldierssetup

  install -d "${pkgdir}${_install_path}"
  install -d "${pkgdir}/usr/bin/"
  install -Dm755 "swordsandsoldiers" "swordsandsoldierssetup" "${pkgdir}/usr/bin/"
  install -Dm755 "SwordsAndSoldiers.bin" "SwordsAndSoldiersSetup.bin" "${pkgdir}${_install_path}"
  install -Dm644 "README" "SwordsAndSoldiersSetup.png" "SwordsAndSoldiers.png" "${pkgdir}${_install_path}"

  cp -dpr --no-preserve=ownership "Data" "${pkgdir}/opt/${pkgname}"

  install -Dm644 "SwordsAndSoldiers.desktop" "${pkgdir}/usr/share/applications/SwordsAndSoldiers.desktop"
  install -Dm644 "SwordsAndSoldiersSetup.desktop" "${pkgdir}/usr/share/applications/SwordsAndSoldiersSetup.desktop"
}

# vim:set ts=2 sw=2 et:
