# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Jose Javier  <jojapa@terra.es>

pkgname=tftp-hpa
pkgver=5.2
pkgrel=2
pkgdesc="Official tftp server"
arch=('i686' 'x86_64')
url="http://www.kernel.org/pub/software/network/tftp/"
license=('BSD')
depends=('readline>=6.0.00')
conflicts=('netkit-tftp')
backup=('etc/conf.d/tftpd')
source=(http://www.kernel.org/pub/software/network/tftp/tftp-hpa/$pkgname-$pkgver.tar.gz 
       tftpd.service
       tftpd.socket
       LICENSE
       tftp-hpa-0.49-fortify-strcpy-crash.patch)
md5sums=('3de3038e7c2bf6fc5d496825893ac8e7'
         '165b98f814f74568635a720e674d7f18'
         'a23369ea33be8b4a5427ec2cfc5373dd'
         '6ce21e27b6fdc1a1adf85c81e42aeecf'
         '22e8629ef19bc276a102c5d4d284c1bd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i ../tftp-hpa-0.49-fortify-strcpy-crash.patch
  ./configure --prefix=/usr --mandir=/usr/share/man --without-tcpwrappers 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" 
  make INSTALLROOT="${pkgdir}" install
  install -d "${pkgdir}/var/tftpboot"
  install -D -m655 "${srcdir}/tftpd.service" "${pkgdir}/usr/lib/systemd/system/tftpd.service"
  install -D -m655 "${srcdir}/tftpd.socket" "${pkgdir}/usr/lib/systemd/system/tftpd.socket"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm "${pkgdir}/usr/share/man/man8/tftpd.8"
}
