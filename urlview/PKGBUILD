# Maintainer:  Ram-Z <ram-z[at]chakraos[dot]org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Gris Ge <cnfourt@gmail.com>

pkgname=urlview
_pkgver=0.9
_patchlevel=20
pkgver=${_pkgver}p$_patchlevel
pkgrel=1
pkgdesc="A curses URL parser for text files"
arch=('i686' 'x86_64')
url="http://packages.qa.debian.org/u/urlview.html"
license=('GPL')
depends=('bash' 'automake' 'ncurses')
source=(http://ftp.debian.org/debian/pool/main/u/urlview/${pkgname}_$_pkgver.orig.tar.gz \
        http://ftp.debian.org/debian/pool/main/u/urlview/${pkgname}_$_pkgver-$_patchlevel.diff.gz)
md5sums=('4f253c82b59136181b1f0bf218ef059d'
         '189268d9e7f818b18183e89fb3369a8c')

prepare() {
  cd "${srcdir}"/$pkgname-$_pkgver.orig

  patch -Np1 -i "${srcdir}"/${pkgname}_${_pkgver}-$_patchlevel.diff
  aclocal
  automake --foreign --ignore-deps --add-missing
}

build() {
  cd "${srcdir}"/$pkgname-$_pkgver.orig

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$_pkgver.orig

  install -d "${pkgdir}"/etc/urlview "${pkgdir}"/usr/share/man/man1
  make prefix="${pkgdir}"/usr mandir="${pkgdir}"/usr/share/man install

  install -Dm755 url_handler.sh "${pkgdir}"/etc/urlview/url_handler.sh
  install -Dm644 sample.urlview "${pkgdir}"/etc/urlview/system.urlview
  install -d "${pkgdir}"/usr/bin
  ln -fs /etc/urlview/url_handler.sh "${pkgdir}"/usr/bin/url_handler.sh
}
