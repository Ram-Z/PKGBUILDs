# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Wojciech Siewierski <darkvifon at gmail dot com>
pkgname=urxvt-autocomplete-all-the-things-git
_gitname="autocomplete-ALL-the-things"
pkgver=1.5.2.r1.g4c911b4
pkgrel=1
pkgdesc="an urxvt plugin allowing user to easily complete words"
url="https://github.com/Vifon/autocomplete-ALL-the-things"
arch=('any')
license=('GPL')
depends=('rxvt-unicode')
makedepends=('git')
install=autocomplete-ALL-the-things.install
source=("git://github.com/Vifon/autocomplete-ALL-the-things.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

package() {
  cd $_gitname
  install -D -m 644 "autocomplete-ALL-the-things" "$pkgdir/usr/lib/urxvt/perl/autocomplete-ALL-the-things"
}
