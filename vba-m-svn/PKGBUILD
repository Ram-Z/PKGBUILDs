# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)

pkgname=vba-m-svn
_pkgname=vbam
pkgver=1226
pkgrel=1
pkgdesc="A VBA fork that focuses on consolidating existing ports and improving VBA in general"
arch=(x86_64)
url="http://vba-m.ngemu.com"
license=('GPL')
depends=('gtkmm' 'sdl' 'glibmm' 'libpng' 'zlib' 'cairo' 'mesa' 'gtkglext' 'gtkglextmm' 'libxv' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'pkg-config' 'nasm' 'subversion')
install='vba-m.install'
source=("${pkgname}::svn+https://$_pkgname.svn.sourceforge.net/svnroot/$_pkgname/trunk")
md5sums=('SKIP')

pkgver() {
  cd $pkgname

  svnversion | tr -d [A-z]
}

build() {
  cd $pkgname

  export PKG_CONFIG_PATH=/usr/lib/ffmpeg-compat/pkgconfig/:$PKG_CONFIG_PATH

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATA_INSTALL_DIR:PATH=share/vbam/gtk \
    -DENABLE_WX=OFF \
    -DENABLE_LINK=OFF \
    -DCMAKE_CXX_FLAGS=" -fpermissive"
  make
}

package() {
  cd $pkgname

  make DESTDIR=$pkgdir install
}

# vim: sts=2 ts=2 sw=2 et
