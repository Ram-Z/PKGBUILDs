# Maintainer:  Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: SÃ©bastien Luttringer

pkgname=vicious
pkgver=2.1.3
pkgrel=1
pkgdesc='Widgets for the Awesome window manager'
arch=('any')
url='http://git.sysphere.org/vicious/about/'
license=('GPL2')
depends=('lua')
optdepends=(
  'hddtemp: for the HDD Temp widget type'
  'alsa-utils: for the Volume widget type'
  'wireless_tools: for the Wireless widget type'
  'curl: for widget types accessing network resources'
)
source=("http://git.sysphere.org/vicious/snapshot/vicious-$pkgver.tar.xz")
md5sums=('457c286dae9ec4870f3349bf7cbff0f7')

package() {
  cd $pkgname-$pkgver
  # Install the vicious library
  install -dm755 "$pkgdir"/usr/share/awesome/lib/$pkgname/{widgets,contrib}
  install -m644 *.lua "$pkgdir/usr/share/awesome/lib/$pkgname"
  install -m644 widgets/*.lua "$pkgdir/usr/share/awesome/lib/$pkgname/widgets"
  install -m644 contrib/*.lua "$pkgdir/usr/share/awesome/lib/$pkgname/contrib"
  # Install documentation
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 README CHANGES "$pkgdir/usr/share/doc/$pkgname"
}
