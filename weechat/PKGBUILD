# Maintainer:  Samir Benmendil <samir.benmendil@gmail.com>
# Contributor: msx <archlinux.us: msx>
# Source     : https://www.archlinux.org/packages/extra/x86_64/weechat/
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=1.7
pkgrel=1
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('x86_64')
url="http://www.weechat.org/"
screenshot="http://i.imgur.com/sMKk1zZ.png"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoc' 'source-highlight' 'cmake' 'pkg-config' 'perl'
             'python2' 'lua' 'tcl' 'ruby' 'aspell' 'guile')
optdepends=('perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell' 'guile')
source=("http://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('593e836e164e75520cbf0442154e59d6')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -Wno-dev ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DENABLE_MAN=ON \
        -DENABLE_DOC=ON
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}/" install
}
