# Maintainer: Ram-Z <ram-z@chakra-project.org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Florian Pritz <f-p@gmx.at>
# Contributor: dorphell <dorphell@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=xautolock
pkgver=2.2
pkgrel=3
pkgdesc="An automatic X screen-locker/screen-saver"
arch=(i686 x86_64)
url="ftp://ibiblio.org/pub/Linux/X11/screensavers/"
license=('GPL2')
depends=('libxss')
makedepends=('imake')
source=("http://ibiblio.org/pub/Linux/X11/screensavers/$pkgname-$pkgver.tgz"
        "max-time.patch")
md5sums=('9526347a202694ad235d731d9d3de91f'
         '45663ba5b2578fd153ff3f8d58899a31')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i "$srcdir"/max-time.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  xmkmf
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install.man
}
