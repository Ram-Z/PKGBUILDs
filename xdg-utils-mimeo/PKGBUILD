# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: matthewbauer <mjbauer95+aur@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: cantabile <cantabile dot desu at gmail dot com>

pkgname=xdg-utils-mimeo
pkgver=1.1.0.rc1.r101.g34e0338
pkgrel=1
pkgdesc='Command line tools that assist applications with a variety of desktop integration tasks; patched to use mimeo'
arch=('any')
url='http://portland.freedesktop.org/'
license=('MIT')
depends=('sh' 'xorg-xset')
makedepends=('docbook-xsl' 'lynx' 'xmlto' 'git')
optdepends=('kdebase-runtime: for KDE support in xdg-open'
        'libgnome: for GNOME support in xdg-open'
        'exo: for Xfce support in xdg-open'
        'xorg-xprop: for Xfce support in xdg-open'
        'pcmanfm: for LXDE support in xdg-open'
        'mimeo: for mimeo support in xdg-open'
        'perl-file-mimeinfo: for generic support in xdg-open')
provides=('xdg-utils')
conflicts=('xdg-utils')
source=("git://anongit.freedesktop.org/xdg/xdg-utils#commit=34e03384"
        mimeo-detection.diff)
md5sums=('SKIP'
         '8d2bc063f1599ec0a959875614263910')

pkgver() {
    cd xdg-utils
    git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd xdg-utils
    patch -p1 -i "${srcdir}"/mimeo-detection.diff
}

build() {
    cd xdg-utils
    ./configure --prefix=/usr --mandir=/usr/share/man
    # make scripts-clean -C scripts
    # make man scripts -C scripts
    make
}

package() {
    cd xdg-utils
    make DESTDIR="${pkgdir}" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # install empty directory to prevent xdg-desktop-menu install *.desktop to fail, see FS#33316
    install -dm755 $pkgdir/usr/share/desktop-directories
}
