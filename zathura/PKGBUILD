# Maintainer:  Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

pkgname=zathura
pkgver=0.3.9
pkgrel=1
pkgdesc="Minimalistic document viewer"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/zathura/"
license=('custom')
depends=('girara>=0.2.5' 'sqlite3' 'desktop-file-utils' 'file' 'libsynctex')
makedepends=('meson' 'python2-docutils' 'texlive-bin')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support')
install=zathura.install
source=(http://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.xz
        bash-completion)
sha1sums=('23fd07120031f64a52c24eb90b6e0de9397ba011'
          '94a8236c483626a7f3acee053a1ea885aed45a82')

build() {
  cd zathura-$pkgver
  chakra-meson build

  cd build
  ninja
}

package() {
  cd zathura-$pkgver/build

  DESTDIR="$pkgdir" ninja install
  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm0644 "$srcdir"/bash-completion "$pkgdir"/usr/share/bash-completion/completions/zathura
}
