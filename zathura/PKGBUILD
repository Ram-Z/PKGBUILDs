# Maintainer:  Samir Benmendil <samir.benmendil[at]gmail[dot]com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

pkgname=zathura
pkgver=0.3.7
pkgrel=1
pkgdesc="Minimalistic document viewer"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/zathura/"
license=('custom')
depends=('girara>=0.2.5' 'sqlite3' 'desktop-file-utils' 'file' 'libsynctex')
makedepends=('python2-docutils' 'texlive-bin')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support')
install=zathura.install
source=(http://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.gz
        bash-completion)
sha1sums=('51935ae1781712b9dc8baed7698c2fa900bcf4fa'
          '94a8236c483626a7f3acee053a1ea885aed45a82')

build() {
  cd zathura-$pkgver
  make WITH_SYNCTEX=1
}

package() {
  cd zathura-$pkgver

  make install DESTDIR=$pkgdir
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 $srcdir/bash-completion $pkgdir/usr/share/bash-completion/completions/zathura
}
